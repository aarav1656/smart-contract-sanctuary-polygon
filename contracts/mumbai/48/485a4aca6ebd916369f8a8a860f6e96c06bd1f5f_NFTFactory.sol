// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

import {Clones} from "@openzeppelin/contracts/proxy/Clones.sol";
import {Ownable} from "@openzeppelin/contracts/access/Ownable.sol";
import {Address} from "@openzeppelin/contracts/utils/Address.sol";
import {INFT} from "./interfaces/INFT.sol";
import {IFacilitator} from "./interfaces/IFacilitator.sol";
import {INFTFactory} from "./interfaces/INFTFactory.sol";
import {IRoyaltyDistributorFactory} from "./interfaces/IRoyaltyDistributorFactory.sol";

contract NFTFactory is Ownable, INFTFactory {
    //------------------- Errors ---------------------//

    error NoContractAddress();
    error ZeroAddressFacilitator();
    error UnOrderedTokenIds();
    error ZeroAddress();

    //------------------- State variables ----------------//

    /// Address of the logic implementation address.
    address public proxyImplementation;

    /// Address of the Royality distributor factory address.
    IRoyaltyDistributorFactory public immutable royaltyDistributorFactory;

    /// Address of the facilitator contract
    /// used for the primary market sales.
    address public facilitator;

    /// Address of the clones produced by the factory.
    address[] private _createdClones;

    /// Cheap storage technique
    address public globalRoyaltyReceiver;

    /// Percentage of royalty that get charged during sales of NFT.
    uint96 public feeNumerator;

    /// Authorised signer for the mutation.
    address public authorisedMutationSigner;

    /// Approver contract address.
    address public approver;

    /// InterfaceId of mutagen.
    bytes4 public mutagenInterfaceId;

    //------------------ Events -----------------------//

    /// Event emission when new nft aka clone got created.
    event NFTCreated(
        string name, string symbol, string baseUri, address cloneAddress
    );

    /// Emitted when facilitator get set.
    event FacilitatorSet(address newFacilitator);

    /// Emit when implementation contract address get changed.
    event ImplementationChanged(
        address oldImplementation, address newImplementation
    );

    /// Emit when the global royalty info is set.
    event SetGlobalRoyaltyInfo(
        address globalRoyaltyReceiver, uint96 feeNumerator
    );

    /// Emit when authorised mutation signer changes.
    event AuthorisedMutationSignerChanged(address _newSigner);

    /// Emit when mutagen interface id changes.
    event MutagenInterfaceIdChanged(bytes4 newInterfaceId);

    /// Emit when approver address changes.
    event ApproverAddressChanged(address _newApprover);

    /// Initialization of the contract.
    /// @param _proxyImplementation Address of the logic contract which get used
    ///                             to create the clones.
    constructor(address _proxyImplementation, IRoyaltyDistributorFactory _royaltyDistributorFactory) {
        proxyImplementation = _proxyImplementation;
        royaltyDistributorFactory = _royaltyDistributorFactory;
    }

    /// @notice Allows the owner to set the facilitator.
    /// @param _facilitator Address of the primary market contract.
    function setFacilitator(address _facilitator) external onlyOwner {
        if (_facilitator == address(0)) {
            revert ZeroAddressFacilitator();
        }
        facilitator = _facilitator;
        emit FacilitatorSet(_facilitator);
    }

    /// @notice Facilitates the creation of the NFT aka clone, Only owner is allowed to call.
    /// @param  _baseUri Base uri of the NFT.
    /// @param  _name Name of the NFT.
    /// @param  _symbol Symbol of the NFT
    /// @param  _series List of series supported by the primary sale.
    /// @param  _mintPrices List of purchase price or mint price that user has to pay for
    /// @param  _maxTokenIdForSeries Maximum tokenId supported for different series. (Should be sorted in order).
    /// @param  _payees Addresses of the royalty receivers.
    /// @param  _shares Share percentages of each royalty receivers address.
    /// @param  _royaltyMetadata[0] Default royalty percentage and _royaltyMetadata[1] Max royalty percentage.
    /// @param _tokenAddress address(1) if native currency else ERC20 token address.
    /// @param _oracleCurrency Currency pair to get price from oracle.
    function createNFT(
        string memory _baseUri,
        string memory _name,
        string memory _symbol,
        string[] memory _series,
        uint256[] memory _mintPrices,
        uint256[] memory _maxTokenIdForSeries,
        address[] memory _payees,
        uint256[] memory _shares,
        uint96[2] memory _royaltyMetadata, 
        address _tokenAddress,
        string memory _oracleCurrency
    )
        external
        onlyOwner
        returns (address clone)
    {
        // Create clone
        clone = Clones.clone(proxyImplementation);
        address _royaltyDefaultReceiver;
        // This will set the RoyaltyDistributor contract as _royaltyDefaultReceiver.
        // If payees doesn't exist then _royaltyDefaultReceiver will become zero.
        if (_payees.length != 0 && _payees.length == _shares.length) {
            _royaltyDefaultReceiver = royaltyDistributorFactory.createRoyaltyDistributor(clone, msg.sender, _payees, _shares);
        }
        // Initialize the contract
        INFT(clone).initialize(
            _maxTokenIdForSeries.length == 1
                ? _maxTokenIdForSeries[0]
                : _getMaxTokenId(_maxTokenIdForSeries),
            _baseUri,
            _name,
            _symbol,
            msg.sender, // TODO: discuss who should be initial owner
            facilitator,
            _royaltyDefaultReceiver,
            _royaltyMetadata[0],
            _royaltyMetadata[1]
        );
        _createdClones.push(clone);
        // Add NFT in primary market
        IFacilitator(facilitator).addNFTInPrimaryMarket(
            clone,
            _mintPrices,
            _series,
            _maxTokenIdForSeries,
            _tokenAddress,
            _oracleCurrency
        );
        // Emit event for NFT creation.
        emit NFTCreated(_name, _symbol, _baseUri, clone);
    }

    /// @notice Set global royalty that can be used by the NFTs.
    /// @dev Can only be set by the owner.
    /// @param  _receiver Address of the royalty receiver.
    /// @param  _feeNumerator Fee fraction that will get charge for every sale.
    function setGlobalRoyaltyInfo(address _receiver, uint96 _feeNumerator)
        external
        onlyOwner
    {
        require(
            _feeNumerator <= 10000, "ERC2981: royalty fee will exceed salePrice"
        );
        require(_receiver != address(0), "ERC2981: invalid receiver");
        globalRoyaltyReceiver = _receiver;
        feeNumerator = _feeNumerator;
        emit SetGlobalRoyaltyInfo(globalRoyaltyReceiver, feeNumerator);
    }

    /// @notice Retrieve the global royalty info.
    function getGlobalRoyaltyInfo()
        external
        view
        returns (GlobalRoyaltyInfo memory)
    {
        return _getGlobalRoyaltyInfo();
    }

    /// @notice return all the create clones.
    function getClones() external view returns (address[] memory) {
        return _createdClones;
    }

    /// @notice Change implementation address, Owner of the contract has the authorisation
    /// to change.
    /// @param newImplementation Address of the new logic contract address.
    function changeImplementation(address newImplementation)
        external
        onlyOwner
    {
        if (!Address.isContract(newImplementation)) {
            revert NoContractAddress();
        }
        emit ImplementationChanged(proxyImplementation, newImplementation);
        proxyImplementation = newImplementation;
    }

    /// @notice Allow to change the authorised mutation signer. Only owner can change that.
    /// @param newAuthorisedSigner New address set as the authorised signer.
    function changeAuthorisedMutationSigner(address newAuthorisedSigner)
        external
        onlyOwner
    {
        if (newAuthorisedSigner == address(0)) {
            revert ZeroAddress();
        }
        authorisedMutationSigner = newAuthorisedSigner;
        emit AuthorisedMutationSignerChanged(newAuthorisedSigner);
    }

    /// @notice Allow to change the mutagen interface id. Only owner can change that.
    /// @param newInterfaceId New interface id of mutagen.
    function changeMutagenInterfaceId(bytes4 newInterfaceId)
        external
        onlyOwner
    {
        mutagenInterfaceId = newInterfaceId;
        emit MutagenInterfaceIdChanged(newInterfaceId);
    }

    /// @notice Allow to change the mutation approver contract address. Only owner can change that.
    /// @param newApprover Address of the approver contract.
    function changeApproverAddress(address newApprover)
        external
        onlyOwner
    {
        approver = newApprover;
        emit ApproverAddressChanged(newApprover);
    }

    function _getGlobalRoyaltyInfo()
        internal
        view
        returns (GlobalRoyaltyInfo memory)
    {
        return GlobalRoyaltyInfo(globalRoyaltyReceiver, feeNumerator);
    }

    function _getMaxTokenId(uint256[] memory _maxTokenIds)
        internal
        pure
        returns (uint256 _maxTokenId)
    {
        uint256 len = _maxTokenIds.length;
        for (uint256 i = 0; i < len - 1; i++) {
            if (_maxTokenIds[i + 1] < _maxTokenIds[i]) {
                revert UnOrderedTokenIds();
            }
        }
        _maxTokenId = _maxTokenIds[len - 1];
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (proxy/Clones.sol)

pragma solidity ^0.8.0;

/**
 * @dev https://eips.ethereum.org/EIPS/eip-1167[EIP 1167] is a standard for
 * deploying minimal proxy contracts, also known as "clones".
 *
 * > To simply and cheaply clone contract functionality in an immutable way, this standard specifies
 * > a minimal bytecode implementation that delegates all calls to a known, fixed address.
 *
 * The library includes functions to deploy a proxy using either `create` (traditional deployment) or `create2`
 * (salted deterministic deployment). It also includes functions to predict the addresses of clones deployed using the
 * deterministic method.
 *
 * _Available since v3.4._
 */
library Clones {
    /**
     * @dev Deploys and returns the address of a clone that mimics the behaviour of `implementation`.
     *
     * This function uses the create opcode, which should never revert.
     */
    function clone(address implementation) internal returns (address instance) {
        /// @solidity memory-safe-assembly
        assembly {
            let ptr := mload(0x40)
            mstore(ptr, 0x3d602d80600a3d3981f3363d3d373d3d3d363d73000000000000000000000000)
            mstore(add(ptr, 0x14), shl(0x60, implementation))
            mstore(add(ptr, 0x28), 0x5af43d82803e903d91602b57fd5bf30000000000000000000000000000000000)
            instance := create(0, ptr, 0x37)
        }
        require(instance != address(0), "ERC1167: create failed");
    }

    /**
     * @dev Deploys and returns the address of a clone that mimics the behaviour of `implementation`.
     *
     * This function uses the create2 opcode and a `salt` to deterministically deploy
     * the clone. Using the same `implementation` and `salt` multiple time will revert, since
     * the clones cannot be deployed twice at the same address.
     */
    function cloneDeterministic(address implementation, bytes32 salt) internal returns (address instance) {
        /// @solidity memory-safe-assembly
        assembly {
            let ptr := mload(0x40)
            mstore(ptr, 0x3d602d80600a3d3981f3363d3d373d3d3d363d73000000000000000000000000)
            mstore(add(ptr, 0x14), shl(0x60, implementation))
            mstore(add(ptr, 0x28), 0x5af43d82803e903d91602b57fd5bf30000000000000000000000000000000000)
            instance := create2(0, ptr, 0x37, salt)
        }
        require(instance != address(0), "ERC1167: create2 failed");
    }

    /**
     * @dev Computes the address of a clone deployed using {Clones-cloneDeterministic}.
     */
    function predictDeterministicAddress(
        address implementation,
        bytes32 salt,
        address deployer
    ) internal pure returns (address predicted) {
        /// @solidity memory-safe-assembly
        assembly {
            let ptr := mload(0x40)
            mstore(ptr, 0x3d602d80600a3d3981f3363d3d373d3d3d363d73000000000000000000000000)
            mstore(add(ptr, 0x14), shl(0x60, implementation))
            mstore(add(ptr, 0x28), 0x5af43d82803e903d91602b57fd5bf3ff00000000000000000000000000000000)
            mstore(add(ptr, 0x38), shl(0x60, deployer))
            mstore(add(ptr, 0x4c), salt)
            mstore(add(ptr, 0x6c), keccak256(ptr, 0x37))
            predicted := keccak256(add(ptr, 0x37), 0x55)
        }
    }

    /**
     * @dev Computes the address of a clone deployed using {Clones-cloneDeterministic}.
     */
    function predictDeterministicAddress(address implementation, bytes32 salt)
        internal
        view
        returns (address predicted)
    {
        return predictDeterministicAddress(implementation, salt, address(this));
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verify that a low level call to smart-contract was successful, and revert (either by bubbling
     * the revert reason or using the provided one) in case of unsuccessful call or if target was not a contract.
     *
     * _Available since v4.8._
     */
    function verifyCallResultFromTarget(
        address target,
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        if (success) {
            if (returndata.length == 0) {
                // only check isContract if the call was successful and the return data is empty
                // otherwise we already know that it was a contract
                require(isContract(target), "Address: call to non-contract");
            }
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    /**
     * @dev Tool to verify that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason or using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    function _revert(bytes memory returndata, string memory errorMessage) private pure {
        // Look for revert reason and bubble it up if present
        if (returndata.length > 0) {
            // The easiest way to bubble the revert reason is using memory via assembly
            /// @solidity memory-safe-assembly
            assembly {
                let returndata_size := mload(returndata)
                revert(add(32, returndata), returndata_size)
            }
        } else {
            revert(errorMessage);
        }
    }
}

// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

import {IERC721} from "@openzeppelin/contracts/token/ERC721/IERC721.sol";

interface INFT is IERC721 {
    /// @notice Initialize the NFT collection.
    /// @param _maxSupply maximum supply of a collection.
    /// @param baseUri base Url of the nft's metadata.
    /// @param _name name of the collection.
    /// @param _symbol symbol of the collection.
    /// @param _owner owner of the collection.
    /// @param _minter Address of the minter allowed to mint tokenIds.
    /// @param _royaltyReceiver Beneficary of the royalty.
    /// @param _feeNumerator Percentage of fee charged as royalty.
    /// @param _maxRoyaltyPercentage Percentage of maximum fee charged as royalty.
    function initialize(
        uint256 _maxSupply,
        string calldata baseUri,
        string calldata _name,
        string calldata _symbol,
        address _owner,
        address _minter,
        address _royaltyReceiver,
        uint96 _feeNumerator,
        uint96 _maxRoyaltyPercentage
    )
        external;

    /// @notice Mint a token and assign it to an address.
    /// @param _to NFT transferred to the given address.
    /// @param _tokenURI URI for token metadata.
    function mint(address _to, string memory _tokenURI) external;

    /// @notice Sets the royalty information that all ids in this contract will default to.
    /// Requirements:
    /// `receiver` cannot be the zero address.
    /// `feeNumerator` cannot be greater than the fee denominator.
    function setDefaultRoyalty(address receiver, uint96 feeNumerator) external;

    /// @notice Sets the royalty information for a specific token id, overriding the global default.
    /// Requirements:
    /// `receiver` cannot be the zero address.
    /// `feeNumerator` cannot be greater than the fee denominator.
    /// @param tokenId Token identitifer whom royalty information gonna set.
    /// @param receiver Beneficiary of the royalty.
    /// @param feeNumerator Percentage of fee gonna charge as royalty.
    function setTokenRoyalty(
        uint256 tokenId,
        address receiver,
        uint96 feeNumerator
    )
        external;

    /// @notice Deletes the default royalty information.
    function deleteDefaultRoyalty() external;

    /// @notice Global royalty would not be in use after this.
    function closeGlobalRoyalty() external;

    /// @notice Global royalty would be in use after this.
    function openGlobalRoyalty() external;

    /// @notice Resets royalty information for the token id back to the global default.
    function resetTokenRoyalty(uint256 tokenId) external;

    /// @notice Returns the URI that provides the details of royalty for OpenSea support.
    /// Ref - https://docs.opensea.io/v2.0/docs/contract-level-metadata
    function contractURI() external view returns (string memory);

    /// @notice Returns the Uniform Resource Identifier (URI) for `tokenId` token.
    /// @param tokenId Identifier for the token
    function tokenURI(uint256 tokenId)
        external
        view
        returns (string memory);
    
    /// @notice Returns the base URI for the contract.
    function baseURI() external view returns (string memory);

    /// @notice Set the base URI, Only ADMIN can call it.
    /// @param newBaseUri New base uri for the metadata.
    function setBaseUri(string memory newBaseUri) external;

    /// @notice Set the token URI for the given tokenId.
    /// @param tokenId Identifier for the token
    /// @param tokenUri URI for the given tokenId.
    function setTokenUri(uint256 tokenId, string memory tokenUri) external;

    function nextTokenId() external view returns (uint256);

    function maximumSupply() external view returns (uint256);

    function globalRoyaltyInEffect() external view returns (bool);

    function owner() external view returns (address);
}

// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

interface IFacilitator {
    /// @notice Function to provide the ownership of the minting of the given nft.
    /// @param nft Address of the nft whose purchase would be allowed.
    /// @param basePrices Base prices of the NFT during the primary sales for different series.
    /// @param series Supoorted series for a given nft sale.
    /// @param maxTokenIdForSeries Maximum tokenId supported for different series. (Should be sorted in order).
    /// @param tokenAddress Token address if currency is ERC20.
    /// @param oracleCurrencyPair Currency pair to get price from oracle.
    function addNFTInPrimaryMarket(
        address nft,
        uint256[] calldata basePrices,
        string[] calldata series,
        uint256[] calldata maxTokenIdForSeries,
        address tokenAddress,
        string calldata oracleCurrencyPair
    )
        external;

    /// @notice Allow the owner to remove the given NFT from the listings.
    /// @param nft Address of the NFT that needs to be unlisted.
    function removeNFTFromPrimaryMarket(address nft) external;

    /// @notice Allow a user to purchase the NFTs in batch.
    /// @param nft Address of the NFT which need to get purcahse.
    /// @param receiver Address of the receiver.
    /// @param tokenURIs URIs for the tokenIds that get minted.
    /// @param expiry Expiry of the signature.
    /// @param signature Offchain signature of the authorised address.
    /// @param amount Amount of tokens if currency is ERC20.
    function batchPurchaseNFT(
        address nft,
        address receiver,
        uint256 expiry,
        bytes memory signature,
        uint256 amount,
        string[] memory tokenURIs
    )
        external
        payable;
}

// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

interface INFTFactory {
    struct GlobalRoyaltyInfo {
        address globalRoyaltyReceiver;
        uint96 feeNumerator;
    }

    function getGlobalRoyaltyInfo()
        external
        view
        returns (GlobalRoyaltyInfo memory);

    function authorisedMutationSigner() external view returns (address);

    function approver() external view returns (address);

    function mutagenInterfaceId() external view returns (bytes4);
}

// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

interface IRoyaltyDistributorFactory {
    function createRoyaltyDistributor(
        address royaltyOf,
        address owner,
        address[] calldata payees,
        uint256[] calldata shares
    )
        external
        returns (address);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC721/IERC721.sol)

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165.sol";

/**
 * @dev Required interface of an ERC721 compliant contract.
 */
interface IERC721 is IERC165 {
    /**
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     */
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.
     */
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the number of tokens in ``owner``'s account.
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes calldata data
    ) external;

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must have been allowed to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Transfers `tokenId` token from `from` to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {safeTransferFrom} whenever possible.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) external;

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool _approved) external;

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}