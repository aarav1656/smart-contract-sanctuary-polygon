// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (security/Pausable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract Pausable is Context {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    constructor() {
        _paused = false;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        _requireNotPaused();
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        _requirePaused();
        _;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Throws if the contract is paused.
     */
    function _requireNotPaused() internal view virtual {
        require(!paused(), "Pausable: paused");
    }

    /**
     * @dev Throws if the contract is not paused.
     */
    function _requirePaused() internal view virtual {
        require(paused(), "Pausable: not paused");
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.8.0) (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        _nonReentrantBefore();
        _;
        _nonReentrantAfter();
    }

    function _nonReentrantBefore() private {
        // On the first call to nonReentrant, _status will be _NOT_ENTERED
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;
    }

    function _nonReentrantAfter() private {
        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (token/ERC1155/IERC1155.sol)

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165.sol";

/**
 * @dev Required interface of an ERC1155 compliant contract, as defined in the
 * https://eips.ethereum.org/EIPS/eip-1155[EIP].
 *
 * _Available since v3.1._
 */
interface IERC1155 is IERC165 {
    /**
     * @dev Emitted when `value` tokens of token type `id` are transferred from `from` to `to` by `operator`.
     */
    event TransferSingle(address indexed operator, address indexed from, address indexed to, uint256 id, uint256 value);

    /**
     * @dev Equivalent to multiple {TransferSingle} events, where `operator`, `from` and `to` are the same for all
     * transfers.
     */
    event TransferBatch(
        address indexed operator,
        address indexed from,
        address indexed to,
        uint256[] ids,
        uint256[] values
    );

    /**
     * @dev Emitted when `account` grants or revokes permission to `operator` to transfer their tokens, according to
     * `approved`.
     */
    event ApprovalForAll(address indexed account, address indexed operator, bool approved);

    /**
     * @dev Emitted when the URI for token type `id` changes to `value`, if it is a non-programmatic URI.
     *
     * If an {URI} event was emitted for `id`, the standard
     * https://eips.ethereum.org/EIPS/eip-1155#metadata-extensions[guarantees] that `value` will equal the value
     * returned by {IERC1155MetadataURI-uri}.
     */
    event URI(string value, uint256 indexed id);

    /**
     * @dev Returns the amount of tokens of token type `id` owned by `account`.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function balanceOf(address account, uint256 id) external view returns (uint256);

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {balanceOf}.
     *
     * Requirements:
     *
     * - `accounts` and `ids` must have the same length.
     */
    function balanceOfBatch(address[] calldata accounts, uint256[] calldata ids)
        external
        view
        returns (uint256[] memory);

    /**
     * @dev Grants or revokes permission to `operator` to transfer the caller's tokens, according to `approved`,
     *
     * Emits an {ApprovalForAll} event.
     *
     * Requirements:
     *
     * - `operator` cannot be the caller.
     */
    function setApprovalForAll(address operator, bool approved) external;

    /**
     * @dev Returns true if `operator` is approved to transfer ``account``'s tokens.
     *
     * See {setApprovalForAll}.
     */
    function isApprovedForAll(address account, address operator) external view returns (bool);

    /**
     * @dev Transfers `amount` tokens of token type `id` from `from` to `to`.
     *
     * Emits a {TransferSingle} event.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - If the caller is not `from`, it must have been approved to spend ``from``'s tokens via {setApprovalForAll}.
     * - `from` must have a balance of tokens of type `id` of at least `amount`.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
     * acceptance magic value.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes calldata data
    ) external;

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {safeTransferFrom}.
     *
     * Emits a {TransferBatch} event.
     *
     * Requirements:
     *
     * - `ids` and `amounts` must have the same length.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the
     * acceptance magic value.
     */
    function safeBatchTransferFrom(
        address from,
        address to,
        uint256[] calldata ids,
        uint256[] calldata amounts,
        bytes calldata data
    ) external;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC1155/IERC1155Receiver.sol)

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165.sol";

/**
 * @dev _Available since v3.1._
 */
interface IERC1155Receiver is IERC165 {
    /**
     * @dev Handles the receipt of a single ERC1155 token type. This function is
     * called at the end of a `safeTransferFrom` after the balance has been updated.
     *
     * NOTE: To accept the transfer, this must return
     * `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))`
     * (i.e. 0xf23a6e61, or its own function selector).
     *
     * @param operator The address which initiated the transfer (i.e. msg.sender)
     * @param from The address which previously owned the token
     * @param id The ID of the token being transferred
     * @param value The amount of tokens being transferred
     * @param data Additional data with no specified format
     * @return `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))` if transfer is allowed
     */
    function onERC1155Received(
        address operator,
        address from,
        uint256 id,
        uint256 value,
        bytes calldata data
    ) external returns (bytes4);

    /**
     * @dev Handles the receipt of a multiple ERC1155 token types. This function
     * is called at the end of a `safeBatchTransferFrom` after the balances have
     * been updated.
     *
     * NOTE: To accept the transfer(s), this must return
     * `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`
     * (i.e. 0xbc197c81, or its own function selector).
     *
     * @param operator The address which initiated the batch transfer (i.e. msg.sender)
     * @param from The address which previously owned the token
     * @param ids An array containing ids of each token being transferred (order and length must match values array)
     * @param values An array containing amounts of each token being transferred (order and length must match ids array)
     * @param data Additional data with no specified format
     * @return `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))` if transfer is allowed
     */
    function onERC1155BatchReceived(
        address operator,
        address from,
        uint256[] calldata ids,
        uint256[] calldata values,
        bytes calldata data
    ) external returns (bytes4);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC1155/utils/ERC1155Holder.sol)

pragma solidity ^0.8.0;

import "./ERC1155Receiver.sol";

/**
 * Simple implementation of `ERC1155Receiver` that will allow a contract to hold ERC1155 tokens.
 *
 * IMPORTANT: When inheriting this contract, you must include a way to use the received tokens, otherwise they will be
 * stuck.
 *
 * @dev _Available since v3.1._
 */
contract ERC1155Holder is ERC1155Receiver {
    function onERC1155Received(
        address,
        address,
        uint256,
        uint256,
        bytes memory
    ) public virtual override returns (bytes4) {
        return this.onERC1155Received.selector;
    }

    function onERC1155BatchReceived(
        address,
        address,
        uint256[] memory,
        uint256[] memory,
        bytes memory
    ) public virtual override returns (bytes4) {
        return this.onERC1155BatchReceived.selector;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC1155/utils/ERC1155Receiver.sol)

pragma solidity ^0.8.0;

import "../IERC1155Receiver.sol";
import "../../../utils/introspection/ERC165.sol";

/**
 * @dev _Available since v3.1._
 */
abstract contract ERC1155Receiver is ERC165, IERC1155Receiver {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC165, IERC165) returns (bool) {
        return interfaceId == type(IERC1155Receiver).interfaceId || super.supportsInterface(interfaceId);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)

pragma solidity ^0.8.0;

import "./IERC165.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.14;

/*///////////////////////////////////////
─────────────────────────────────────────
──┌───┐───────────────────┌───┬───┬───┐──
──│┌─┐│───────────────────└┐┌┐│┌─┐│┌─┐│──
──│└─┘├──┬─┬──┬──┬──┬─┐┌──┐│││││─│││─││──
──│┌──┤┌┐│┌┤┌┐│┌┐│┌┐│┌┐┤──┤││││└─┘││─││──
──││──│┌┐│││┌┐│└┘│└┘│││├──├┘└┘│┌─┐│└─┘│──
──└┘──└┘└┴┘└┘└┴─┐├──┴┘└┴──┴───┴┘─└┴───┘──
──────────────┌─┘│───────────────────────
──────────────└──┘───────────────────────
─────────────────────────────────────────
///////////////////////////////////////*/

interface IAddressRegistry {
    function uriGenerator() external view returns (address);

    function treasury() external view returns (address);

    function pdp() external view returns (address);

    function pxp() external view returns (address);

    function pdt() external view returns (address);

    function pdtOracle() external view returns (address);

    function playerMgmt() external view returns (address);

    function poolMgmt() external view returns (address);

    function svgGenerator() external view returns (address);
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.14;

/*///////////////////////////////////////
─────────────────────────────────────────
──┌───┐───────────────────┌───┬───┬───┐──
──│┌─┐│───────────────────└┐┌┐│┌─┐│┌─┐│──
──│└─┘├──┬─┬──┬──┬──┬─┐┌──┐│││││─│││─││──
──│┌──┤┌┐│┌┤┌┐│┌┐│┌┐│┌┐┤──┤││││└─┘││─││──
──││──│┌┐│││┌┐│└┘│└┘│││├──├┘└┘│┌─┐│└─┘│──
──└┘──└┘└┴┘└┘└┴─┐├──┴┘└┴──┴───┴┘─└┴───┘──
──────────────┌─┘│───────────────────────
──────────────└──┘───────────────────────
─────────────────────────────────────────
///////////////////////////////////////*/

interface IAssetPool {
    struct UserAssetInfo {
        address asset;
        uint256 tokenId;
        uint256 amount;
    }

    function setPid(uint256 _pid) external;

    function isActivatedAsset(address _asset) external returns (bool);

    function liquidityByAsset(address _asset) external returns (uint256);

    function borrowsByAsset(address _asset) external returns (uint256);

    function updateBorrows(address _asset, uint256 _amount, bool _isIncrease) external;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.14;

import "./IAssetPool.sol";

/*///////////////////////////////////////
─────────────────────────────────────────
──┌───┐───────────────────┌───┬───┬───┐──
──│┌─┐│───────────────────└┐┌┐│┌─┐│┌─┐│──
──│└─┘├──┬─┬──┬──┬──┬─┐┌──┐│││││─│││─││──
──│┌──┤┌┐│┌┤┌┐│┌┐│┌┐│┌┐┤──┤││││└─┘││─││──
──││──│┌┐│││┌┐│└┘│└┘│││├──├┘└┘│┌─┐│└─┘│──
──└┘──└┘└┴┘└┘└┴─┐├──┴┘└┴──┴───┴┘─└┴───┘──
──────────────┌─┘│───────────────────────
──────────────└──┘───────────────────────
─────────────────────────────────────────
///////////////////////////////////////*/

interface IAssetPool1155 is IAssetPool {

}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.14;

/*///////////////////////////////////////
─────────────────────────────────────────
──┌───┐───────────────────┌───┬───┬───┐──
──│┌─┐│───────────────────└┐┌┐│┌─┐│┌─┐│──
──│└─┘├──┬─┬──┬──┬──┬─┐┌──┐│││││─│││─││──
──│┌──┤┌┐│┌┤┌┐│┌┐│┌┐│┌┐┤──┤││││└─┘││─││──
──││──│┌┐│││┌┐│└┘│└┘│││├──├┘└┘│┌─┐│└─┘│──
──└┘──└┘└┴┘└┘└┴─┐├──┴┘└┴──┴───┴┘─└┴───┘──
──────────────┌─┘│───────────────────────
──────────────└──┘───────────────────────
─────────────────────────────────────────
///////////////////////////////////////*/

interface IPoolManagement {
    struct PoolInfo {
        string poolName;
        address assetPool721;
        address assetPool1155;
        address playerRental;
        address riskStrategy;
    }

    function isActivatedPool(uint256 _pid) external view returns (bool);

    function getPoolInfo(uint256 _pid) external view returns (string memory, address, address, address, address);

    function poolCount() external view returns (uint256);
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.14;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/security/Pausable.sol";
import "@openzeppelin/contracts/token/ERC1155/utils/ERC1155Holder.sol";
import "@openzeppelin/contracts/utils/introspection/IERC165.sol";
import "@openzeppelin/contracts/token/ERC1155/IERC1155.sol";

import "../interfaces/IAssetPool1155.sol";
import "../interfaces/IAddressRegistry.sol";
import "../interfaces/IPoolManagement.sol";

/*///////////////////////////////////////
─────────────────────────────────────────
──┌───┐───────────────────┌───┬───┬───┐──
──│┌─┐│───────────────────└┐┌┐│┌─┐│┌─┐│──
──│└─┘├──┬─┬──┬──┬──┬─┐┌──┐│││││─│││─││──
──│┌──┤┌┐│┌┤┌┐│┌┐│┌┐│┌┐┤──┤││││└─┘││─││──
──││──│┌┐│││┌┐│└┘│└┘│││├──├┘└┘│┌─┐│└─┘│──
──└┘──└┘└┴┘└┘└┴─┐├──┴┘└┴──┴───┴┘─└┴───┘──
──────────────┌─┘│───────────────────────
──────────────└──┘───────────────────────
─────────────────────────────────────────
///////////////////////////////////////*/

contract AssetPool1155 is IAssetPool1155, Ownable, ReentrancyGuard, Pausable, ERC1155Holder {
    /// @dev Pool Id
    uint256 public pid;

    /// @dev Asset list that can be used in the pool
    address[] public poolAssets;

    /// @dev Asset -> Bool
    mapping(address => bool) public isAssetExist;

    /// @dev Asset -> Bool
    mapping(address => bool) public isActivatedAsset;

    /// @dev User asset info
    mapping(address => UserAssetInfo[]) public userAsset;

    /// @dev User -> Asset -> TokenId -> Balance
    mapping(address => mapping(address => mapping(uint256 => uint256))) public userBalance;

    /// @dev User -> Asset -> TokenId -> UserAssetInfo index
    /// index of userAsset array, starts from 1 to avoid confusion with the non-existing asset
    mapping(address => mapping(address => mapping(uint256 => uint256))) public userAssetId;

    /// @dev Asset counts, `totalLiquidity` = `totalBorrows` + `remainingAmount`
    uint256 public totalLiquidity;
    uint256 public totalBorrows;
    mapping(address => uint256) public liquidityByAsset;
    mapping(address => uint256) public borrowsByAsset;

    /// @notice AddressRegstry contract
    IAddressRegistry public addressRegistry;

    bytes4 private constant INTERFACE_ID_ERC1155 = 0xd9b67a26;

    error OnlyPoolManagement(address by);
    error OnlyPlayerRental(address by);
    error ZeroAddress();
    error AssetAlreadyExist(address asset);
    error AssetNotExist(address asset);
    error AssetAlreadyActivated(address asset);
    error AssetAlreadyDeactivated(address asset);
    error DeactivatedPool(uint256 pid);
    error DeactivatedAsset(address asset);
    error NotERC1155(address asset);
    error AssetNotMatched();
    error UnexpectedAssetWithdrawal(address asset);
    error InsufficientUserBalance(uint256 userBalance, uint256 withdrawAmount);

    event AssetAdded(address indexed asset);
    event AssetActivated(address indexed asset);
    event AssetDeactivated(address indexed asset);
    event Deposited(address indexed by, address indexed asset, uint256[] tokenIds, uint256[] amounts);
    event Withdrawn(address indexed by, address indexed asset, uint256[] tokenIds, uint256[] amounts);

    modifier onlyActivatedPool() {
        if (!IPoolManagement(addressRegistry.poolMgmt()).isActivatedPool(pid)) revert DeactivatedPool(pid);
        _;
    }

    modifier onlyActivatedAsset(address _asset) {
        if (!isActivatedAsset[_asset]) revert DeactivatedAsset(_asset);
        _;
    }

    modifier onlyPlayerRental() {
        (, , , address playerRental, ) = IPoolManagement(addressRegistry.poolMgmt()).getPoolInfo(pid);
        if (msg.sender != playerRental) revert OnlyPlayerRental(msg.sender);
        _;
    }

    constructor(address _addressRegistry) {
        if (_addressRegistry == address(0)) revert ZeroAddress();

        addressRegistry = IAddressRegistry(_addressRegistry);
    }

    function setPid(uint256 _pid) external {
        if (msg.sender != addressRegistry.poolMgmt()) revert OnlyPoolManagement(msg.sender);
        pid = _pid;
    }

    function addPoolAsset(address _asset) external onlyOwner {
        if (_asset == address(0)) revert ZeroAddress();
        if (isAssetExist[_asset]) revert AssetAlreadyExist(_asset);
        if (!IERC165(_asset).supportsInterface(INTERFACE_ID_ERC1155)) revert NotERC1155(_asset);

        // add a new pool
        poolAssets.push(_asset);

        isAssetExist[_asset] = true;
        isActivatedAsset[_asset] = true;

        emit AssetAdded(_asset);
    }

    function activateAsset(address _asset) external onlyOwner {
        if (!isAssetExist[_asset]) revert AssetNotExist(_asset);
        if (isActivatedAsset[_asset]) revert AssetAlreadyActivated(_asset);

        isActivatedAsset[_asset] = true;
        totalLiquidity += liquidityByAsset[_asset];

        emit AssetActivated(_asset);
    }

    function deactivateAsset(address _asset) external onlyOwner {
        if (!isAssetExist[_asset]) revert AssetNotExist(_asset);
        if (!isActivatedAsset[_asset]) revert AssetAlreadyDeactivated(_asset);

        isActivatedAsset[_asset] = false;
        totalLiquidity -= liquidityByAsset[_asset];

        emit AssetDeactivated(_asset);
    }

    function deposit(
        address _asset,
        uint256[] calldata _tokenIds,
        uint256[] calldata _amounts
    ) external onlyActivatedPool onlyActivatedAsset(_asset) nonReentrant whenNotPaused {
        // get the token count
        uint256 tokenCount = _tokenIds.length;

        // check the asset param
        if (_amounts.length != tokenCount) revert AssetNotMatched();

        // deposit the assets
        IERC1155(_asset).safeBatchTransferFrom(msg.sender, address(this), _tokenIds, _amounts, bytes(""));

        // update the user balance and totalLiquidity
        uint256 totalAmount;
        for (uint256 i; i < tokenCount; i++) {
            uint256 tokenId = _tokenIds[i];

            // update UserAssetInfo
            uint256 userAssetIndex = userAssetId[msg.sender][_asset][tokenId];
            if (userAssetIndex == 0) {
                // add a new UserAssetInfo
                userAsset[msg.sender].push(UserAssetInfo({asset: _asset, tokenId: tokenId, amount: _amounts[i]}));
                userAssetId[msg.sender][_asset][tokenId] = userAsset[msg.sender].length; // userAssetId starts from 1
            } else {
                // update the existing UserAssetInfo
                userAsset[msg.sender][userAssetIndex - 1].amount += _amounts[i];
            }

            // update userBalance and totalLiquidity
            userBalance[msg.sender][_asset][tokenId] += _amounts[i];
            totalAmount += _amounts[i];

            // check if the updated user balance is matched
            userAssetIndex = userAssetId[msg.sender][_asset][tokenId];
            require(
                userAsset[msg.sender][userAssetIndex - 1].amount == userBalance[msg.sender][_asset][tokenId],
                "IncorrectUserBalance"
            );
        }
        totalLiquidity += totalAmount;
        liquidityByAsset[_asset] += totalAmount;

        emit Deposited(msg.sender, _asset, _tokenIds, _amounts);
    }

    function withdraw(
        address _asset,
        uint256[] calldata _tokenIds,
        uint256[] calldata _amounts
    ) external nonReentrant whenNotPaused {
        // get the token
        uint256 tokenCount = _tokenIds.length;

        // check the asset param
        if (_amounts.length != tokenCount) revert AssetNotMatched();

        // update the user balance and totalLiquidity
        uint256 totalAmount;
        for (uint256 i; i < tokenCount; i++) {
            uint256 tokenId = _tokenIds[i];

            // update UserAssetInfo
            uint256 userAssetIndex = userAssetId[msg.sender][_asset][tokenId];
            if (userAssetIndex == 0) revert UnexpectedAssetWithdrawal(_asset);

            userAsset[msg.sender][userAssetIndex - 1].amount -= _amounts[i];

            // update userBalance and totalLiquidity
            uint256 userAmount = userBalance[msg.sender][_asset][tokenId];
            if (userAmount < _amounts[i]) revert InsufficientUserBalance(userAmount, _amounts[i]);

            userBalance[msg.sender][_asset][tokenId] -= _amounts[i];
            totalAmount += _amounts[i];

            // check if the updated user balance is matched
            require(
                userAsset[msg.sender][userAssetIndex - 1].amount == userBalance[msg.sender][_asset][tokenId],
                "IncorrectUserBalance"
            );
        }
        totalLiquidity -= totalAmount;
        liquidityByAsset[_asset] -= totalAmount;

        // withdraw the assets
        IERC1155(_asset).safeBatchTransferFrom(address(this), msg.sender, _tokenIds, _amounts, bytes(""));

        emit Withdrawn(msg.sender, _asset, _tokenIds, _amounts);
    }

    function updateBorrows(address _asset, uint256 _amount, bool _isIncrease) external onlyPlayerRental {
        if (_isIncrease) {
            borrowsByAsset[_asset] += _amount;
            totalBorrows += _amount;
        } else {
            borrowsByAsset[_asset] -= _amount;
            totalBorrows -= _amount;
        }
    }

    /// @notice Pause the asset pool
    function pause() external onlyOwner {
        _pause();
    }

    /// @notice Unpause the asset pool
    function unpause() external onlyOwner {
        _unpause();
    }
}