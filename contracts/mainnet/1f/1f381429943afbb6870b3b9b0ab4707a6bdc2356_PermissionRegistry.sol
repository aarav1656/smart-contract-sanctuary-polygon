// SPDX-License-Identifier: GPL-3.0
// Docgen-SOLC: 0.8.15

pragma solidity ^0.8.15;

interface IOwned {
  function owner() external view returns (address);

  function nominatedOwner() external view returns (address);

  function nominateNewOwner(address owner) external;

  function acceptOwnership() external;
}

// SPDX-License-Identifier: GPL-3.0
// Docgen-SOLC: 0.8.15

pragma solidity ^0.8.15;

import { IOwned } from "../IOwned.sol";

struct Permission {
  bool endorsed;
  bool rejected;
}

interface IPermissionRegistry is IOwned {
  function setPermissions(address[] calldata targets, Permission[] calldata newPermissions) external;

  function endorsed(address target) external view returns (bool);

  function rejected(address target) external view returns (bool);
}

// SPDX-License-Identifier: GPL-3.0
// Docgen-SOLC: 0.8.15

pragma solidity ^0.8.15;

// https://docs.synthetix.io/contracts/source/contracts/owned
contract Owned {
  address public owner;
  address public nominatedOwner;

  constructor(address _owner) {
    require(_owner != address(0), "Owner address cannot be 0");
    owner = _owner;
    emit OwnerChanged(address(0), _owner);
  }

  function nominateNewOwner(address _owner) external virtual onlyOwner {
    nominatedOwner = _owner;
    emit OwnerNominated(_owner);
  }

  function acceptOwnership() external virtual {
    require(msg.sender == nominatedOwner, "You must be nominated before you can accept ownership");
    emit OwnerChanged(owner, nominatedOwner);
    owner = nominatedOwner;
    nominatedOwner = address(0);
  }

  modifier onlyOwner() {
    _onlyOwner();
    _;
  }

  function _onlyOwner() private view {
    require(msg.sender == owner, "Only the contract owner may perform this action");
  }

  event OwnerNominated(address newOwner);
  event OwnerChanged(address oldOwner, address newOwner);
}

// SPDX-License-Identifier: GPL-3.0
// Docgen-SOLC: 0.8.15

pragma solidity ^0.8.15;

import { Owned } from "../utils/Owned.sol";
import { Permission } from "../interfaces/vault/IPermissionRegistry.sol";

/**
 * @title   PermissionRegistry
 * @author  RedVeil
 * @notice  Allows the DAO to endorse and reject addresses for security purposes.
 */
contract PermissionRegistry is Owned {
  /*//////////////////////////////////////////////////////////////
                            IMMUTABLES
    //////////////////////////////////////////////////////////////*/

  /// @param _owner `AdminProxy`
  constructor(address _owner) Owned(_owner) {}

  /*//////////////////////////////////////////////////////////////
                          PERMISSIONS
    //////////////////////////////////////////////////////////////*/

  mapping(address => Permission) public permissions;

  event PermissionSet(address target, bool newEndorsement, bool newRejection);

  error Mismatch();

  /**
   * @notice Set permissions for an array of target. Caller must be owner. (`VaultController` via `AdminProxy`)
   * @param targets `AdminProxy`
   * @param newPermissions An array of permissions to set for the targets.
   * @dev A permission can never be both endorsed and rejected.
   */
  function setPermissions(address[] calldata targets, Permission[] calldata newPermissions) external onlyOwner {
    uint256 len = targets.length;
    if (len != newPermissions.length) revert Mismatch();

    for (uint256 i = 0; i < len; i++) {
      if (newPermissions[i].endorsed && newPermissions[i].rejected) revert Mismatch();

      emit PermissionSet(targets[i], newPermissions[i].endorsed, newPermissions[i].rejected);

      permissions[targets[i]] = newPermissions[i];
    }
  }

  function endorsed(address target) external view returns (bool) {
    return permissions[target].endorsed;
  }

  function rejected(address target) external view returns (bool) {
    return permissions[target].rejected;
  }
}