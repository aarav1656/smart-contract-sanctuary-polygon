/**
 *Submitted for verification at polygonscan.com on 2023-04-11
*/

// File: interfaces/ILaunchpad.sol


pragma solidity 0.8.9;

interface ILaunchpad {
    struct LaunchpadData {
        bool isActive;
        uint32 maxPerUser;
        uint32 maxRegister;
        uint32 maxRedeem;
        address whitelistAuthority;
        address feeRedeemAddress;
        uint256 feeRedeem;
        uint256 registerStartTimestamp;
        uint256 registerEndTimestamp;
        uint256 redeemStartTimestamp;
        uint256 redeemEndTimestamp;
        uint256 claimStartTimestamp;
    }

    function setLaunchpad(LaunchpadData memory launchpadData) external;

    event SetLaunchpad(LaunchpadData data);
    event SetLaunchpadStatus(bool isActive);
    event Register(address user);
    event Redeem(address user, uint256 amount);
    event Claim(address user);
    event SetProtocolFee(uint256 protocolFee);
    event SetSharingFee(uint256 sharingFee);
}
// File: interfaces/ITransferableLaunchpad.sol


pragma solidity 0.8.9;


interface ITransferableLaunchpad is ILaunchpad {
    event RedeemToken(address user, uint256[] tokenIds);
    event PendingToken(address user, uint256[] tokenIds);
    event PushToken(address tokenAddress, uint256[] tokenIds);
    event WithdrawNFT(address tokenAddress, uint256[] tokenIds);
    
    function init(address nftAddress, uint256 sharingFee, uint256 protocolFee) external;
}

// File: interfaces/IMintableLaunchpad.sol


pragma solidity 0.8.9;


interface IMintableLaunchpad is ILaunchpad {
    event RedeemToken(address user, uint256 fromId, uint256 toId);
    event PendingToken(address user, uint256 fromId, uint256 toId);
    event PushToken(address tokenAddress, uint256[] tokenIds);
    event WithdrawNFT(address tokenAddress, uint256[] tokenIds);
    event ConvertToken(address user, uint256 sourceTokenId, address destTokenAddress, uint256 destTokenId);
    event SetConvertInfo(address convertTokenAddress, uint256 convertTime, uint256 convertEndTime);

    function init(string memory name, string memory symbol, string memory baseUri, bool enableMinter, uint256 sharingFee, uint256 protocolFee) external;
}

// File: @openzeppelin/contracts/utils/Address.sol


// OpenZeppelin Contracts (last updated v4.8.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verify that a low level call to smart-contract was successful, and revert (either by bubbling
     * the revert reason or using the provided one) in case of unsuccessful call or if target was not a contract.
     *
     * _Available since v4.8._
     */
    function verifyCallResultFromTarget(
        address target,
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        if (success) {
            if (returndata.length == 0) {
                // only check isContract if the call was successful and the return data is empty
                // otherwise we already know that it was a contract
                require(isContract(target), "Address: call to non-contract");
            }
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    /**
     * @dev Tool to verify that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason or using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    function _revert(bytes memory returndata, string memory errorMessage) private pure {
        // Look for revert reason and bubble it up if present
        if (returndata.length > 0) {
            // The easiest way to bubble the revert reason is using memory via assembly
            /// @solidity memory-safe-assembly
            assembly {
                let returndata_size := mload(returndata)
                revert(add(32, returndata), returndata_size)
            }
        } else {
            revert(errorMessage);
        }
    }
}

// File: @openzeppelin/contracts/token/ERC20/extensions/draft-IERC20Permit.sol


// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/draft-IERC20Permit.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 Permit extension allowing approvals to be made via signatures, as defined in
 * https://eips.ethereum.org/EIPS/eip-2612[EIP-2612].
 *
 * Adds the {permit} method, which can be used to change an account's ERC20 allowance (see {IERC20-allowance}) by
 * presenting a message signed by the account. By not relying on {IERC20-approve}, the token holder account doesn't
 * need to send a transaction, and thus is not required to hold Ether at all.
 */
interface IERC20Permit {
    /**
     * @dev Sets `value` as the allowance of `spender` over ``owner``'s tokens,
     * given ``owner``'s signed approval.
     *
     * IMPORTANT: The same issues {IERC20-approve} has related to transaction
     * ordering also apply here.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `deadline` must be a timestamp in the future.
     * - `v`, `r` and `s` must be a valid `secp256k1` signature from `owner`
     * over the EIP712-formatted function arguments.
     * - the signature must use ``owner``'s current nonce (see {nonces}).
     *
     * For more information on the signature format, see the
     * https://eips.ethereum.org/EIPS/eip-2612#specification[relevant EIP
     * section].
     */
    function permit(
        address owner,
        address spender,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external;

    /**
     * @dev Returns the current nonce for `owner`. This value must be
     * included whenever a signature is generated for {permit}.
     *
     * Every successful call to {permit} increases ``owner``'s nonce by one. This
     * prevents a signature from being used multiple times.
     */
    function nonces(address owner) external view returns (uint256);

    /**
     * @dev Returns the domain separator used in the encoding of the signature for {permit}, as defined by {EIP712}.
     */
    // solhint-disable-next-line func-name-mixedcase
    function DOMAIN_SEPARATOR() external view returns (bytes32);
}

// File: @openzeppelin/contracts/token/ERC20/IERC20.sol


// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
}

// File: @openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol


// OpenZeppelin Contracts (last updated v4.8.0) (token/ERC20/utils/SafeERC20.sol)

pragma solidity ^0.8.0;




/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    function safeTransfer(
        IERC20 token,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(
        IERC20 token,
        address from,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    function safeIncreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        uint256 newAllowance = token.allowance(address(this), spender) + value;
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }

    function safePermit(
        IERC20Permit token,
        address owner,
        address spender,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) internal {
        uint256 nonceBefore = token.nonces(owner);
        token.permit(owner, spender, value, deadline, v, r, s);
        uint256 nonceAfter = token.nonces(owner);
        require(nonceAfter == nonceBefore + 1, "SafeERC20: permit did not succeed");
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address-functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) {
            // Return data is optional
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}

// File: @openzeppelin/contracts/utils/Context.sol


// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// File: @openzeppelin/contracts/access/Ownable.sol


// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;


/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// File: libraries/AccessControl.sol


pragma solidity 0.8.9;


contract AccessControl is Ownable {
    mapping(address => bool) _creators;

    event SetCreator(address creator, bool isActive);

    function isActiveCreator(address creator) public view returns(bool) {
        return _creators[creator];
    }

    modifier onlyCreator() {
        require(isActiveCreator(_msgSender()), "Launchpad Access Control: caller is not the creator");
        _;
    }

    function setCreator(address creator, bool isActive) external onlyOwner {
        _setCreator(creator, isActive);
    }

    function _setCreator(address creator, bool isActive) internal {
        _creators[creator] = isActive;

        emit SetCreator(creator, isActive);
    }
}

// File: libraries/TimeLock.sol


pragma solidity 0.8.9;


/**
 * @dev Provide mechanism for Time Locking, Owner of contract can unlock this contract, after locking time 
 * owner can execute special function and then contract will be lock again.
 *
 * This contract is only required for intermediate, library-like contracts.
 */
contract TimeLock is AccessControl {
    uint private _lockTime;

    mapping(bytes4 => bool) _isUnlock;
    mapping(bytes4 => uint) _unlockAts;

    event Unlock(bytes4 functionSign, uint timeUnlock);

    /**
     * @dev Initializes the contract setting the deployer as the initial lock time.
     */
    constructor(uint lockTime) {
        _lockTime = lockTime;
    }

    /**
     * @dev Returns contract is unlock.
     */
    function isUnlock(bytes4 functionSign) public virtual view returns(bool) {
        return _isUnlock[functionSign] && (_unlockAts[functionSign] + _lockTime) <= block.timestamp;
    }

    /**
     * @dev Throws if contract is lock, after execute function contract will be lock again.
     */
    modifier whenUnlock() {
        require(isUnlock(msg.sig), "LockSchedule: contract is locked");
        _;
        _isUnlock[msg.sig] = false;
    }

    /**
     * @dev Unlock contract, contract state Lock -> Pending -> Unlock -> Lock.
     */
    function unlock(bytes4 functionSign) external onlyOwner {
        _isUnlock[functionSign] = true;
        _unlockAts[functionSign] = block.timestamp;

        emit Unlock(functionSign, block.timestamp);
    }
}

// File: interfaces/ILaunchpadFactory.sol


pragma solidity 0.8.9;



interface ILaunchpadFactory {
    function getFeeAddress() view external returns(address); 

    event CreateMintableLaunchpad(address launchpadAddress, address owner, string name, string symbol, bool enableMint, ILaunchpad.LaunchpadData data);
    event CreateTransferableLaunchpad(address launchpadAddress, address owner, address nftAddress, ILaunchpad.LaunchpadData data);
    event CreateLaunchpad(bytes32 key, address launchpadAddress, address owner);
    event SetImplement(bytes32 key, address implement);
    event SetFeeAddress(address newFeeAddress);
}

// File: @openzeppelin/contracts/proxy/Clones.sol


// OpenZeppelin Contracts (last updated v4.8.0) (proxy/Clones.sol)

pragma solidity ^0.8.0;

/**
 * @dev https://eips.ethereum.org/EIPS/eip-1167[EIP 1167] is a standard for
 * deploying minimal proxy contracts, also known as "clones".
 *
 * > To simply and cheaply clone contract functionality in an immutable way, this standard specifies
 * > a minimal bytecode implementation that delegates all calls to a known, fixed address.
 *
 * The library includes functions to deploy a proxy using either `create` (traditional deployment) or `create2`
 * (salted deterministic deployment). It also includes functions to predict the addresses of clones deployed using the
 * deterministic method.
 *
 * _Available since v3.4._
 */
library Clones {
    /**
     * @dev Deploys and returns the address of a clone that mimics the behaviour of `implementation`.
     *
     * This function uses the create opcode, which should never revert.
     */
    function clone(address implementation) internal returns (address instance) {
        /// @solidity memory-safe-assembly
        assembly {
            // Cleans the upper 96 bits of the `implementation` word, then packs the first 3 bytes
            // of the `implementation` address with the bytecode before the address.
            mstore(0x00, or(shr(0xe8, shl(0x60, implementation)), 0x3d602d80600a3d3981f3363d3d373d3d3d363d73000000))
            // Packs the remaining 17 bytes of `implementation` with the bytecode after the address.
            mstore(0x20, or(shl(0x78, implementation), 0x5af43d82803e903d91602b57fd5bf3))
            instance := create(0, 0x09, 0x37)
        }
        require(instance != address(0), "ERC1167: create failed");
    }

    /**
     * @dev Deploys and returns the address of a clone that mimics the behaviour of `implementation`.
     *
     * This function uses the create2 opcode and a `salt` to deterministically deploy
     * the clone. Using the same `implementation` and `salt` multiple time will revert, since
     * the clones cannot be deployed twice at the same address.
     */
    function cloneDeterministic(address implementation, bytes32 salt) internal returns (address instance) {
        /// @solidity memory-safe-assembly
        assembly {
            // Cleans the upper 96 bits of the `implementation` word, then packs the first 3 bytes
            // of the `implementation` address with the bytecode before the address.
            mstore(0x00, or(shr(0xe8, shl(0x60, implementation)), 0x3d602d80600a3d3981f3363d3d373d3d3d363d73000000))
            // Packs the remaining 17 bytes of `implementation` with the bytecode after the address.
            mstore(0x20, or(shl(0x78, implementation), 0x5af43d82803e903d91602b57fd5bf3))
            instance := create2(0, 0x09, 0x37, salt)
        }
        require(instance != address(0), "ERC1167: create2 failed");
    }

    /**
     * @dev Computes the address of a clone deployed using {Clones-cloneDeterministic}.
     */
    function predictDeterministicAddress(
        address implementation,
        bytes32 salt,
        address deployer
    ) internal pure returns (address predicted) {
        /// @solidity memory-safe-assembly
        assembly {
            let ptr := mload(0x40)
            mstore(add(ptr, 0x38), deployer)
            mstore(add(ptr, 0x24), 0x5af43d82803e903d91602b57fd5bf3ff)
            mstore(add(ptr, 0x14), implementation)
            mstore(ptr, 0x3d602d80600a3d3981f3363d3d373d3d3d363d73)
            mstore(add(ptr, 0x58), salt)
            mstore(add(ptr, 0x78), keccak256(add(ptr, 0x0c), 0x37))
            predicted := keccak256(add(ptr, 0x43), 0x55)
        }
    }

    /**
     * @dev Computes the address of a clone deployed using {Clones-cloneDeterministic}.
     */
    function predictDeterministicAddress(address implementation, bytes32 salt)
        internal
        view
        returns (address predicted)
    {
        return predictDeterministicAddress(implementation, salt, address(this));
    }
}

// File: LaunchpadFactory.sol


pragma solidity 0.8.9;










/// @title Launchpad Factory
/// @notice Deploys launchpads and manage ownership and control fee
contract LaunchpadFactory is ILaunchpadFactory, TimeLock {
    using SafeERC20 for IERC20; 

    address private _feeAddress;
    uint256 private _totalLaunchpad;

    /// store implement for each launchpad type, use Minimal Proxy to create launchpad
    mapping(bytes32 => address) private _launchpadImplements;

    constructor(address feeAddress, uint256 lockTime, address mintableLaunchpadImplement, address transferableLaunchpadImplement) TimeLock(lockTime) {
        _feeAddress = feeAddress;

        /// keccak256(abi.encodePacked("Mintable"))
        _setLaunchpadImplement(0x444f313053c893c305c4a5f333f3b033d548405c830016c4b623e787aa045145, mintableLaunchpadImplement);

        /// keccak256(abi.encodePacked("Transferable"))
        _setLaunchpadImplement(0x1bc7992855b26a5ae511e9b448c90941ef8f1b835f4936d94c8cfd9202da9384, transferableLaunchpadImplement);
    }

    /**
     * @dev Internal function set launchpad implement for key.
     */
    function _setLaunchpadImplement(bytes32 key, address implement) internal {
        _launchpadImplements[key] = implement;
    }

    /**
     * @dev Internal function clone launchpad.
     */
    function _cloneLaunchpad(bytes32 implementKey) internal returns(address launchpad) {
        address implement = _launchpadImplements[implementKey];

        launchpad = Clones.cloneDeterministic(implement, bytes32(_totalLaunchpad));

        _totalLaunchpad++;
    }

    /**
     * @dev Get protocol fee receiver.
     */
    function getFeeAddress() view external returns(address) {
        return _feeAddress;
    }

    /**
     * @dev Set protocol fee receiver.
     */
    function setFeeAddress(address newFeeAddress) external onlyOwner {
        _feeAddress = newFeeAddress;

        emit SetFeeAddress(newFeeAddress);
    }

    /**
     * @dev Create mintable launchpad.
     */
    function createMintableLaunchpad(address owner, string memory name, string memory symbol, string memory baseUri, bool enableMinter, ILaunchpad.LaunchpadData memory data, uint256 sharingFee, uint256 protocolFee) external onlyCreator returns(address) {
        address newLaunchpadAddress = _cloneLaunchpad(0x444f313053c893c305c4a5f333f3b033d548405c830016c4b623e787aa045145);
        IMintableLaunchpad launchpad = IMintableLaunchpad(newLaunchpadAddress);

        launchpad.init(name, symbol, baseUri, enableMinter, sharingFee, protocolFee);
        launchpad.setLaunchpad(data);
        Ownable(newLaunchpadAddress).transferOwnership(owner);

        emit CreateMintableLaunchpad(newLaunchpadAddress, owner, name, symbol, enableMinter, data);
        return newLaunchpadAddress;
    }

    /**
     * @dev Create transferable launchpad.
     */
    function createTransferableLaunchpad(address owner, address nftAddress, ILaunchpad.LaunchpadData memory data, uint256 sharingFee, uint256 protocolFee) external onlyCreator returns(address) {
        address newLaunchpadAddress = _cloneLaunchpad(0x1bc7992855b26a5ae511e9b448c90941ef8f1b835f4936d94c8cfd9202da9384);
        ITransferableLaunchpad launchpad = ITransferableLaunchpad(newLaunchpadAddress);

        launchpad.init(nftAddress, sharingFee, protocolFee);
        launchpad.setLaunchpad(data);
        Ownable(newLaunchpadAddress).transferOwnership(owner);

        emit CreateTransferableLaunchpad(newLaunchpadAddress, owner, nftAddress, data);
        return newLaunchpadAddress;
    }

    /**
     * @dev Set implement for launchpad type.
     */
    function setImplement(bytes32 key, address implement) external whenUnlock onlyOwner {
        _setLaunchpadImplement(key, implement);

        emit SetImplement(key, implement);
    }

    /**
     * @dev Create launchpad.
     */
    function createLaunchpad(address owner, bytes32 key) external onlyCreator returns(address) {
        address newLaunchpadAddress = _cloneLaunchpad(key);
        Ownable(newLaunchpadAddress).transferOwnership(owner);

        emit CreateLaunchpad(key, newLaunchpadAddress, owner);
        return newLaunchpadAddress;
    }

    /**
     * @dev View function return implement address of launchpad type.
     */
    function getLaunchpadImplement(bytes32 key) public view returns(address) {
        return _launchpadImplements[key];
    }

    /**
     * @dev View function return launchpad address of index.
     */
    function getLaunchpadAddress(uint256 index, bytes32 key) external view returns (address) {
        address implement = getLaunchpadImplement(key);
        return Clones.predictDeterministicAddress(implement, bytes32(index));
    }

    /**
     * @dev View function return total launchpad.
     */
    function getTotalLaunchpad() external view returns (uint256) {
        return _totalLaunchpad;
    }

    /**
     * @dev function for owner withdraw fee token.
     */
    function withdrawFungibleToken(address tokenAddress, uint256 amount) external onlyOwner {
        if (tokenAddress == address(0)) {
            (bool sent,) = _msgSender().call{value: amount}("");
            require(sent, "Launchpad: Fail to send ETH");
        } else {
            IERC20 token = IERC20(tokenAddress);
            token.safeTransfer(_msgSender(), amount);
        }
    }

    fallback() external {}
    receive() external payable {}
}