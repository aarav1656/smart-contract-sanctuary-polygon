// SPDX-License-Identifier: MIT
pragma solidity 0.8.17;
import "ISafeERC20.sol";
import "Strings.sol";

contract QPokerSmartWallet {
    /**
     * @dev {ERC20Transfers} will be emitted whenever {transferERC20Shares} called.
     */
    event ERC20Transfers(
        address ERC20ContractAddress,
        uint256 totalAmount,
        uint256 masterAccountShare,
        uint256 primaryAccountShare
    );

    /**
     * @dev {EthLog} will be emitted whenever {fallback} or {receive} called.
     */
    event EthLog(uint256 time, uint256 amount);
    //================= Base Immutable Settings ==================
    /**
     * @dev this settings are immutable which means this variables cannot be changed after deployment.
     * @dev https://docs.soliditylang.org/en/v0.8.17/contracts.html?highlight=immutable#immutable
     */
    address public immutable masterAccount;
    address public immutable primaryAccount;
    uint256 public immutable primaryAccountShare;
    uint256 public immutable totalSharePart;

    //================= Base Immutable Settings ==================

    /**
     * @dev Initializes the contract settings(owner and affiliate address).
     */
    constructor(
        address _primaryAccount,
        uint256 _primaryAccountShare,
        uint256 _totalSharePart
    ) {
        require(_totalSharePart > _primaryAccountShare);
        masterAccount = msg.sender;
        primaryAccount = _primaryAccount;
        primaryAccountShare = _primaryAccountShare;
        totalSharePart = _totalSharePart;
    }

    fallback() external payable {
        emit EthLog(block.timestamp, msg.value);
    }

    receive() external payable {
        emit EthLog(block.timestamp, msg.value);
    }

    /**
     * @notice returns the share in part of {totalPart},
     *         and the result is for the {primaryAccount} address.
     */
    function checkPrimaryAccountShare() public view returns (string memory) {
        return
            string.concat(
                "Primary account share is (",
                Strings.toString(primaryAccountShare),
                "/",
                Strings.toString(totalSharePart),
                ") of total received tokens."
            );
    }

    /**
     * @notice returns the share in part of {totalPart},
     *         and the result is for the {masterAccount} address.
     */
    function checkMasterAccountShare() public view returns (string memory) {
        return
            string.concat(
                "Master account share is (",
                Strings.toString(totalSharePart - primaryAccountShare),
                "/",
                Strings.toString(totalSharePart),
                ") of total received tokens."
            );
    }

    /**
     * @notice this function handles the safeTransfer specified amount of token
     *  between {this contract} and {to} wallet address.
     * @dev Returns the ERC20 {transfer} function status, if it was 'True'
     *  it means that the transaction succeeded otherwise it will thrown an exception.
     * @param erc20ContractAddress is the address of the ERC20 token.
     * @param to                   is the address of the receiver wallet.
     * @param amount               is the amount of tokens in order to transfer
     *                                from this contract to 'to' wallet.
     */
    function safeERC20TransferFrom(
        address erc20ContractAddress,
        address to,
        uint256 amount
    ) internal {
        (bool success, ) = erc20ContractAddress.call(
            abi.encodeWithSelector(ISafeERC20.transfer.selector, to, amount)
        );

        require(success, "safeERC20Transfer failed.");
    }

    /**
     * @notice ERC20 token balance of an Ethereum account from an ERC20 smart contract.
     * @param contractAddress The address of the ERC20 token smart contract.
     * @return balanceOfAccount is the ERC20 token balance of the account.
     */
    function safeERC20BalanceOf(
        address contractAddress
    ) internal view returns (uint256 balanceOfAccount) {
        // calling the 'balanceOf' function of the ERC20 token contract using the staticcall method.
        (bool success, bytes memory data) = contractAddress.staticcall(
            abi.encodeWithSelector(ISafeERC20.balanceOf.selector, address(this))
        );
        // If the ERC20 contract call is successful, then return the decoded balance value.
        require(success, "erc20 {balanceOf} error.");
        balanceOfAccount = abi.decode(data, (uint256));
    }

    function shareCalculator(
        uint256 totalBalance
    )
        internal
        view
        returns (
            uint256 primaryAccountShareTokens,
            uint256 masterAccountShareTokens
        )
    {
        require(totalBalance > 0, "balance is 0");
        primaryAccountShareTokens =
            (totalBalance * primaryAccountShare) /
            totalSharePart;
        masterAccountShareTokens = totalBalance - primaryAccountShareTokens;
    }

    /**
     * @dev divides the available balance of {erc20ContractAddress} ERC20 Token in the smart contract between {masterAccount} and {primaryAccount}
     */
    function transferERC20Shares(address erc20ContractAddress) public {
        uint256 totalBalance = safeERC20BalanceOf(erc20ContractAddress);
        (
            uint256 primaryAccountShareTokens,
            uint256 masterAccountShareTokens
        ) = shareCalculator(totalBalance);
        safeERC20TransferFrom(
            erc20ContractAddress,
            masterAccount,
            masterAccountShareTokens
        );
        safeERC20TransferFrom(
            erc20ContractAddress,
            primaryAccount,
            primaryAccountShareTokens
        );
        emit ERC20Transfers(
            erc20ContractAddress,
            totalBalance,
            masterAccountShareTokens,
            primaryAccountShareTokens
        );
    }

    /**
     * @notice eth means the main currency of the deployed chain (e.g if this contract deployed on polygon mainnet eth means $Matic token).
     * @dev transfers {amount} of eth to {receiver}
     */
    function transferEth(address receiver, uint256 amount) internal {
        (bool sent, ) = payable(receiver).call{value: amount}("");
        require(sent, "Failed to send Ether");
    }

    /**
     * @dev divides the available balance of Eth in the smart contract between {masterAccount} and {primaryAccount}
     */
    function transferEthShares() public payable {
        uint256 totalBalance = payable(address(this)).balance;
        (
            uint256 primaryAccountShareTokens,
            uint256 masterAccountShareTokens
        ) = shareCalculator(totalBalance);
        transferEth(masterAccount, masterAccountShareTokens);
        transferEth(primaryAccount, primaryAccountShareTokens);
        emit ERC20Transfers(
            address(0),
            totalBalance,
            masterAccountShareTokens,
            primaryAccountShareTokens
        );
    }
}

// SPDX-License-Identifier: MIT
pragma solidity 0.8.17;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface ISafeERC20 {
    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.8.0) (utils/Strings.sol)

pragma solidity ^0.8.0;

import "Math.sol";

/**
 * @dev String operations.
 */
library Strings {
    bytes16 private constant _SYMBOLS = "0123456789abcdef";
    uint8 private constant _ADDRESS_LENGTH = 20;

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        unchecked {
            uint256 length = Math.log10(value) + 1;
            string memory buffer = new string(length);
            uint256 ptr;
            /// @solidity memory-safe-assembly
            assembly {
                ptr := add(buffer, add(32, length))
            }
            while (true) {
                ptr--;
                /// @solidity memory-safe-assembly
                assembly {
                    mstore8(ptr, byte(mod(value, 10), _SYMBOLS))
                }
                value /= 10;
                if (value == 0) break;
            }
            return buffer;
        }
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        unchecked {
            return toHexString(value, Math.log256(value) + 1);
        }
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = _SYMBOLS[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }

    /**
     * @dev Converts an `address` with fixed length of 20 bytes to its not checksummed ASCII `string` hexadecimal representation.
     */
    function toHexString(address addr) internal pure returns (string memory) {
        return toHexString(uint256(uint160(addr)), _ADDRESS_LENGTH);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.8.0) (utils/math/Math.sol)

pragma solidity ^0.8.0;

/**
 * @dev Standard math utilities missing in the Solidity language.
 */
library Math {
    enum Rounding {
        Down, // Toward negative infinity
        Up, // Toward infinity
        Zero // Toward zero
    }

    /**
     * @dev Returns the largest of two numbers.
     */
    function max(uint256 a, uint256 b) internal pure returns (uint256) {
        return a > b ? a : b;
    }

    /**
     * @dev Returns the smallest of two numbers.
     */
    function min(uint256 a, uint256 b) internal pure returns (uint256) {
        return a < b ? a : b;
    }

    /**
     * @dev Returns the average of two numbers. The result is rounded towards
     * zero.
     */
    function average(uint256 a, uint256 b) internal pure returns (uint256) {
        // (a + b) / 2 can overflow.
        return (a & b) + (a ^ b) / 2;
    }

    /**
     * @dev Returns the ceiling of the division of two numbers.
     *
     * This differs from standard division with `/` in that it rounds up instead
     * of rounding down.
     */
    function ceilDiv(uint256 a, uint256 b) internal pure returns (uint256) {
        // (a + b - 1) / b can overflow on addition, so we distribute.
        return a == 0 ? 0 : (a - 1) / b + 1;
    }

    /**
     * @notice Calculates floor(x * y / denominator) with full precision. Throws if result overflows a uint256 or denominator == 0
     * @dev Original credit to Remco Bloemen under MIT license (https://xn--2-umb.com/21/muldiv)
     * with further edits by Uniswap Labs also under MIT license.
     */
    function mulDiv(
        uint256 x,
        uint256 y,
        uint256 denominator
    ) internal pure returns (uint256 result) {
        unchecked {
            // 512-bit multiply [prod1 prod0] = x * y. Compute the product mod 2^256 and mod 2^256 - 1, then use
            // use the Chinese Remainder Theorem to reconstruct the 512 bit result. The result is stored in two 256
            // variables such that product = prod1 * 2^256 + prod0.
            uint256 prod0; // Least significant 256 bits of the product
            uint256 prod1; // Most significant 256 bits of the product
            assembly {
                let mm := mulmod(x, y, not(0))
                prod0 := mul(x, y)
                prod1 := sub(sub(mm, prod0), lt(mm, prod0))
            }

            // Handle non-overflow cases, 256 by 256 division.
            if (prod1 == 0) {
                return prod0 / denominator;
            }

            // Make sure the result is less than 2^256. Also prevents denominator == 0.
            require(denominator > prod1);

            ///////////////////////////////////////////////
            // 512 by 256 division.
            ///////////////////////////////////////////////

            // Make division exact by subtracting the remainder from [prod1 prod0].
            uint256 remainder;
            assembly {
                // Compute remainder using mulmod.
                remainder := mulmod(x, y, denominator)

                // Subtract 256 bit number from 512 bit number.
                prod1 := sub(prod1, gt(remainder, prod0))
                prod0 := sub(prod0, remainder)
            }

            // Factor powers of two out of denominator and compute largest power of two divisor of denominator. Always >= 1.
            // See https://cs.stackexchange.com/q/138556/92363.

            // Does not overflow because the denominator cannot be zero at this stage in the function.
            uint256 twos = denominator & (~denominator + 1);
            assembly {
                // Divide denominator by twos.
                denominator := div(denominator, twos)

                // Divide [prod1 prod0] by twos.
                prod0 := div(prod0, twos)

                // Flip twos such that it is 2^256 / twos. If twos is zero, then it becomes one.
                twos := add(div(sub(0, twos), twos), 1)
            }

            // Shift in bits from prod1 into prod0.
            prod0 |= prod1 * twos;

            // Invert denominator mod 2^256. Now that denominator is an odd number, it has an inverse modulo 2^256 such
            // that denominator * inv = 1 mod 2^256. Compute the inverse by starting with a seed that is correct for
            // four bits. That is, denominator * inv = 1 mod 2^4.
            uint256 inverse = (3 * denominator) ^ 2;

            // Use the Newton-Raphson iteration to improve the precision. Thanks to Hensel's lifting lemma, this also works
            // in modular arithmetic, doubling the correct bits in each step.
            inverse *= 2 - denominator * inverse; // inverse mod 2^8
            inverse *= 2 - denominator * inverse; // inverse mod 2^16
            inverse *= 2 - denominator * inverse; // inverse mod 2^32
            inverse *= 2 - denominator * inverse; // inverse mod 2^64
            inverse *= 2 - denominator * inverse; // inverse mod 2^128
            inverse *= 2 - denominator * inverse; // inverse mod 2^256

            // Because the division is now exact we can divide by multiplying with the modular inverse of denominator.
            // This will give us the correct result modulo 2^256. Since the preconditions guarantee that the outcome is
            // less than 2^256, this is the final result. We don't need to compute the high bits of the result and prod1
            // is no longer required.
            result = prod0 * inverse;
            return result;
        }
    }

    /**
     * @notice Calculates x * y / denominator with full precision, following the selected rounding direction.
     */
    function mulDiv(
        uint256 x,
        uint256 y,
        uint256 denominator,
        Rounding rounding
    ) internal pure returns (uint256) {
        uint256 result = mulDiv(x, y, denominator);
        if (rounding == Rounding.Up && mulmod(x, y, denominator) > 0) {
            result += 1;
        }
        return result;
    }

    /**
     * @dev Returns the square root of a number. If the number is not a perfect square, the value is rounded down.
     *
     * Inspired by Henry S. Warren, Jr.'s "Hacker's Delight" (Chapter 11).
     */
    function sqrt(uint256 a) internal pure returns (uint256) {
        if (a == 0) {
            return 0;
        }

        // For our first guess, we get the biggest power of 2 which is smaller than the square root of the target.
        //
        // We know that the "msb" (most significant bit) of our target number `a` is a power of 2 such that we have
        // `msb(a) <= a < 2*msb(a)`. This value can be written `msb(a)=2**k` with `k=log2(a)`.
        //
        // This can be rewritten `2**log2(a) <= a < 2**(log2(a) + 1)`
        // → `sqrt(2**k) <= sqrt(a) < sqrt(2**(k+1))`
        // → `2**(k/2) <= sqrt(a) < 2**((k+1)/2) <= 2**(k/2 + 1)`
        //
        // Consequently, `2**(log2(a) / 2)` is a good first approximation of `sqrt(a)` with at least 1 correct bit.
        uint256 result = 1 << (log2(a) >> 1);

        // At this point `result` is an estimation with one bit of precision. We know the true value is a uint128,
        // since it is the square root of a uint256. Newton's method converges quadratically (precision doubles at
        // every iteration). We thus need at most 7 iteration to turn our partial result with one bit of precision
        // into the expected uint128 result.
        unchecked {
            result = (result + a / result) >> 1;
            result = (result + a / result) >> 1;
            result = (result + a / result) >> 1;
            result = (result + a / result) >> 1;
            result = (result + a / result) >> 1;
            result = (result + a / result) >> 1;
            result = (result + a / result) >> 1;
            return min(result, a / result);
        }
    }

    /**
     * @notice Calculates sqrt(a), following the selected rounding direction.
     */
    function sqrt(uint256 a, Rounding rounding) internal pure returns (uint256) {
        unchecked {
            uint256 result = sqrt(a);
            return result + (rounding == Rounding.Up && result * result < a ? 1 : 0);
        }
    }

    /**
     * @dev Return the log in base 2, rounded down, of a positive value.
     * Returns 0 if given 0.
     */
    function log2(uint256 value) internal pure returns (uint256) {
        uint256 result = 0;
        unchecked {
            if (value >> 128 > 0) {
                value >>= 128;
                result += 128;
            }
            if (value >> 64 > 0) {
                value >>= 64;
                result += 64;
            }
            if (value >> 32 > 0) {
                value >>= 32;
                result += 32;
            }
            if (value >> 16 > 0) {
                value >>= 16;
                result += 16;
            }
            if (value >> 8 > 0) {
                value >>= 8;
                result += 8;
            }
            if (value >> 4 > 0) {
                value >>= 4;
                result += 4;
            }
            if (value >> 2 > 0) {
                value >>= 2;
                result += 2;
            }
            if (value >> 1 > 0) {
                result += 1;
            }
        }
        return result;
    }

    /**
     * @dev Return the log in base 2, following the selected rounding direction, of a positive value.
     * Returns 0 if given 0.
     */
    function log2(uint256 value, Rounding rounding) internal pure returns (uint256) {
        unchecked {
            uint256 result = log2(value);
            return result + (rounding == Rounding.Up && 1 << result < value ? 1 : 0);
        }
    }

    /**
     * @dev Return the log in base 10, rounded down, of a positive value.
     * Returns 0 if given 0.
     */
    function log10(uint256 value) internal pure returns (uint256) {
        uint256 result = 0;
        unchecked {
            if (value >= 10**64) {
                value /= 10**64;
                result += 64;
            }
            if (value >= 10**32) {
                value /= 10**32;
                result += 32;
            }
            if (value >= 10**16) {
                value /= 10**16;
                result += 16;
            }
            if (value >= 10**8) {
                value /= 10**8;
                result += 8;
            }
            if (value >= 10**4) {
                value /= 10**4;
                result += 4;
            }
            if (value >= 10**2) {
                value /= 10**2;
                result += 2;
            }
            if (value >= 10**1) {
                result += 1;
            }
        }
        return result;
    }

    /**
     * @dev Return the log in base 10, following the selected rounding direction, of a positive value.
     * Returns 0 if given 0.
     */
    function log10(uint256 value, Rounding rounding) internal pure returns (uint256) {
        unchecked {
            uint256 result = log10(value);
            return result + (rounding == Rounding.Up && 10**result < value ? 1 : 0);
        }
    }

    /**
     * @dev Return the log in base 256, rounded down, of a positive value.
     * Returns 0 if given 0.
     *
     * Adding one to the result gives the number of pairs of hex symbols needed to represent `value` as a hex string.
     */
    function log256(uint256 value) internal pure returns (uint256) {
        uint256 result = 0;
        unchecked {
            if (value >> 128 > 0) {
                value >>= 128;
                result += 16;
            }
            if (value >> 64 > 0) {
                value >>= 64;
                result += 8;
            }
            if (value >> 32 > 0) {
                value >>= 32;
                result += 4;
            }
            if (value >> 16 > 0) {
                value >>= 16;
                result += 2;
            }
            if (value >> 8 > 0) {
                result += 1;
            }
        }
        return result;
    }

    /**
     * @dev Return the log in base 10, following the selected rounding direction, of a positive value.
     * Returns 0 if given 0.
     */
    function log256(uint256 value, Rounding rounding) internal pure returns (uint256) {
        unchecked {
            uint256 result = log256(value);
            return result + (rounding == Rounding.Up && 1 << (result * 8) < value ? 1 : 0);
        }
    }
}