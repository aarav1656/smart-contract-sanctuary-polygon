/**
 *Submitted for verification at polygonscan.com on 2023-04-25
*/

// SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.8.0;

//File: [EnumerableSet.sol]

/**
 * @dev Library for managing
 * https://en.wikipedia.org/wiki/Set_(abstract_data_type)[sets] of primitive
 * types.
 *
 * Sets have the following properties:
 *
 * - Elements are added, removed, and checked for existence in constant time
 * (O(1)).
 * - Elements are enumerated in O(n). No guarantees are made on the ordering.
 *
 * ```
 * contract Example {
 *     // Add the library methods
 *     using EnumerableSet for EnumerableSet.AddressSet;
 *
 *     // Declare a set state variable
 *     EnumerableSet.AddressSet private mySet;
 * }
 * ```
 *
 * As of v3.3.0, sets of type `bytes32` (`Bytes32Set`), `address` (`AddressSet`)
 * and `uint256` (`UintSet`) are supported.
 */
library EnumerableSet {
    // To implement this library for multiple types with as little code
    // repetition as possible, we write it in terms of a generic Set type with
    // bytes32 values.
    // The Set implementation uses private functions, and user-facing
    // implementations (such as AddressSet) are just wrappers around the
    // underlying Set.
    // This means that we can only create new EnumerableSets for types that fit
    // in bytes32.

    struct Set {
        // Storage of set values
        bytes32[] _values;
        // Position of the value in the `values` array, plus 1 because index 0
        // means a value is not in the set.
        mapping(bytes32 => uint256) _indexes;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function _add(Set storage set, bytes32 value) private returns (bool) {
        if (!_contains(set, value)) {
            set._values.push(value);
            // The value is stored at length-1, but we add 1 to all indexes
            // and use 0 as a sentinel value
            set._indexes[value] = set._values.length;
            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function _remove(Set storage set, bytes32 value) private returns (bool) {
        // We read and store the value's index to prevent multiple reads from the same storage slot
        uint256 valueIndex = set._indexes[value];

        if (valueIndex != 0) {
            // Equivalent to contains(set, value)
            // To delete an element from the _values array in O(1), we swap the element to delete with the last one in
            // the array, and then remove the last element (sometimes called as 'swap and pop').
            // This modifies the order of the array, as noted in {at}.

            uint256 toDeleteIndex = valueIndex - 1;
            uint256 lastIndex = set._values.length - 1;

            if (lastIndex != toDeleteIndex) {
                bytes32 lastvalue = set._values[lastIndex];

                // Move the last value to the index where the value to delete is
                set._values[toDeleteIndex] = lastvalue;
                // Update the index for the moved value
                set._indexes[lastvalue] = valueIndex; // Replace lastvalue's index to valueIndex
            }

            // Delete the slot where the moved value was stored
            set._values.pop();

            // Delete the index for the deleted slot
            delete set._indexes[value];

            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function _contains(Set storage set, bytes32 value) private view returns (bool) {
        return set._indexes[value] != 0;
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function _length(Set storage set) private view returns (uint256) {
        return set._values.length;
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function _at(Set storage set, uint256 index) private view returns (bytes32) {
        return set._values[index];
    }

    // Bytes32Set

    struct Bytes32Set {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _add(set._inner, value);
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _remove(set._inner, value);
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(Bytes32Set storage set, bytes32 value) internal view returns (bool) {
        return _contains(set._inner, value);
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(Bytes32Set storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(Bytes32Set storage set, uint256 index) internal view returns (bytes32) {
        return _at(set._inner, index);
    }

    // AddressSet

    struct AddressSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(AddressSet storage set, address value) internal returns (bool) {
        return _add(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(AddressSet storage set, address value) internal returns (bool) {
        return _remove(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(AddressSet storage set, address value) internal view returns (bool) {
        return _contains(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(AddressSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(AddressSet storage set, uint256 index) internal view returns (address) {
        return address(uint160(uint256(_at(set._inner, index))));
    }

    // UintSet

    struct UintSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(UintSet storage set, uint256 value) internal returns (bool) {
        return _add(set._inner, bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(UintSet storage set, uint256 value) internal returns (bool) {
        return _remove(set._inner, bytes32(value));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(UintSet storage set, uint256 value) internal view returns (bool) {
        return _contains(set._inner, bytes32(value));
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function length(UintSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(UintSet storage set, uint256 index) internal view returns (uint256) {
        return uint256(_at(set._inner, index));
    }
}

//File: [ReentrancyGuard.sol]

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and make it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

//File: [IToken.sol]

interface IToken
{
	//========================
    // EVENTS FUNCTIONS
    //========================

	event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(address indexed owner, address indexed spender, uint256 value);

	//========================
    // INFO FUNCTIONS
    //========================
	
	function decimals() external view returns (uint8);	
	function symbol() external view returns (string memory);
	function name() external view returns (string memory);
	function totalSupply() external view returns (uint256);
	function allowance(address owner, address spender) external view returns (uint256);

	//========================
    // USER INFO FUNCTIONS
    //========================

    function balanceOf(address account) external view returns (uint256);

    //========================
    // TRANSFER / APPROVE FUNCTIONS
    //========================

    function transfer(address recipient, uint256 amount) external returns (bool);
	function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) external returns (bool);    
    function approve(address spender, uint256 amount) external returns (bool);
}

//File: [IMoonAccessManager.sol]

interface IMoonAccessManager
{
    //========================
    // SECURITY FUNCTIONS
    //========================

    function requireAdmin(address _user) external view;
    function requireManager(address _user) external view;
    function requireSecurityAdmin(address _user) external view;
    function requireSecurityMod(address _user) external view;
    function requireDeployer(address _user) external view;    
}

//File: [Context.sol]

/*
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

//File: [IERC20.sol]

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

//File: [Address.sol]

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        assembly {
            size := extcodesize(account)
        }
        return size > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return _verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return _verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return _verifyCallResult(success, returndata, errorMessage);
    }

    function _verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) private pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

//File: [ML_TransferETH.sol]

contract ML_TransferETH
{
    //========================
    // ATTRIBUTES
    //======================== 

    uint256 public transferGas = 30000;

    //========================
    // CONFIG FUNCTIONS
    //======================== 

    function _setTransferGas(uint256 _gas) internal
    {
        require(_gas >= 30000, "Gas to low");
        require(_gas <= 250000, "Gas to high");
        transferGas = _gas;
    }

    //========================
    // TRANSFER FUNCTIONS
    //======================== 

    function transferETH(address _to, uint256 _amount) internal
    {
        (bool success, ) = payable(_to).call{ value: _amount, gas: transferGas }("");
        success; //prevent warning
    }
}

//File: [Pausable.sol]

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract Pausable is Context {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    constructor() {
        _paused = false;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        require(!paused(), "Pausable: paused");
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        require(paused(), "Pausable: not paused");
        _;
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
}

//File: [MoonAccessRequest.sol]

abstract contract MoonAccessRequest
{
    //========================
    // ATTRIBUTES
    //========================
    
    IMoonAccessManager public immutable accessManager;
    address public owner;

    //========================
    // CONSTRUCT
    //========================

    constructor(
        IMoonAccessManager _accessManager, 
        address _owner
    )
    {   
        accessManager = _accessManager;
        owner = _owner;
    }

    //========================
    // SECURITY FUNCTIONS
    //========================

    function isOwner() internal view returns (bool)
    {
        return (owner == msg.sender);
    }

    function requireOwner() internal view
    {
        require(
            isOwner(),
            "User is not Owner");
    }

    function requireAdmin() internal view
    {
        if (!isOwner())
        {
            accessManager.requireAdmin(msg.sender);
        }
    }

    function requireManager() internal view
    {
        if (!isOwner())
        {
            accessManager.requireManager(msg.sender);
        }
    }

    function requireDeployer() internal view
    {
        if (!isOwner())
        {
            accessManager.requireDeployer(msg.sender);
        }
    }

    function requireSecurityAdmin() internal view
    {
        if (!isOwner())
        {
            accessManager.requireSecurityAdmin(msg.sender);
        }
    }

    function requireSecurityMod() internal view
    {
        if (!isOwner())
        {
            accessManager.requireSecurityMod(msg.sender);
        }
    }
}

//File: [SafeERC20.sol]

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    function safeTransfer(
        IERC20 token,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(
        IERC20 token,
        address from,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    function safeIncreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        uint256 newAllowance = token.allowance(address(this), spender) + value;
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address.functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) {
            // Return data is optional
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}

//File: [ML_RecoverFunds.sol]

contract ML_RecoverFunds is ML_TransferETH
{
    //========================
    // LIBS
    //========================

    using SafeERC20 for IERC20;

    //========================
    // EMERGENCY FUNCTIONS
    //======================== 

    function _recoverETH(uint256 _amount, address _to) internal
    {
        transferETH(_to, _amount);
    }

    function _recoverToken(IToken _token, uint256 _amount, address _to) internal
    {
        IERC20(address(_token)).safeTransfer(_to, _amount);
    }  
}

//File: [ML_TransferHelper.sol]

contract ML_TransferHelper
{
    //========================
    // LIBS
    //========================

    using SafeERC20 for IERC20;

    //========================
    // STRUCTS
    //========================

    struct TransferResult
    {
        uint256 fromBefore;     //balance of from-token, before transfer
        uint256 toBefore;       //balance of token, before transfer
        uint256 toAfter;        //balance of token, after transfer
        uint256 transferred;    //transferred amount 
    }

    //========================
    // FUNCTIONS
    //========================

    function safeTransferFrom(
        IToken _token,
        uint256 _amount,
        address _from,
        address _to
    ) internal returns (TransferResult memory)
    {
        //init
        TransferResult memory result = TransferResult(
        {
            fromBefore: _token.balanceOf(_from),
            toBefore: _token.balanceOf(_to),
            toAfter: 0,
            transferred: 0
        });

        //transfer
        IERC20(address(_token)).safeTransferFrom(
            _from, 
            _to, 
            _amount
        );

        //process
        result.toAfter = _token.balanceOf(_to);
        result.transferred = result.toAfter - result.toBefore;

        return result;
    }

    function safeTransfer(
        IToken _token,
        uint256 _amount,
        address _to
    ) internal returns (TransferResult memory)
    {
        //init
        TransferResult memory result = TransferResult(
        {
            fromBefore: _token.balanceOf(msg.sender),
            toBefore: _token.balanceOf(_to),
            toAfter: 0,
            transferred: 0
        });

        //transfer
        IERC20(address(_token)).safeTransfer(
            _to, 
            _amount
        );

        //process
        result.toAfter = _token.balanceOf(_to);
        result.transferred = result.toAfter - result.toBefore;

        return result;
    }

    function safeApprove(IToken _token, address _spender, uint256 _amount) internal
    {
        _token.approve(_spender, 0); //first reset to 0 to be safe
        if (_amount != 0)
        {
            _token.approve(_spender, _amount);
        }
    }
}

contract Bank is
    MoonAccessRequest,
    Pausable,
    ReentrancyGuard,
    ML_TransferHelper,
    ML_RecoverFunds
{
    //========================
    // LIBS
    //========================

    using SafeERC20 for IToken;
    using EnumerableSet for EnumerableSet.AddressSet;

    //========================
    // STRUCTS
    //========================    

    struct AssetInfo
    {
        IToken token;                               //token
        uint256 balance;                            //balance of token
        uint256 allowances;                         //number of allowances
    }

    struct AllowanceInfo
    {
        address spender;                            //spender address
        uint256 allowance;                          //allowance amount
    }

    struct UserAssetInfo
    {
        uint256 balance;                            //balance of asset
        mapping(address => uint256) allowance;      //allowances
        EnumerableSet.AddressSet allowanceList;     //list of allowances > 0
    }

    struct UserInfo
    {
        mapping(IToken => UserAssetInfo) tokens;    //tokens info (0x0 = Coin)
        EnumerableSet.AddressSet tokenList;         //list of tokens that have balance > 0
        bool preventTransfer;                       //prevents transfer, basicaly freezing assets
    }
    
    //========================
    // CONSTANTS
    //========================

    string public constant VERSION = "1.0.0";
    IToken private constant COIN_ADDRESS = IToken(address(0));

    //========================
    // ATTRIBUTES
    //========================

    mapping(address => UserInfo) private userInfo;
    mapping(IToken => uint256) public totalTokenBalance;
    bool public enableAllowance;

    //========================
    // EVENTS
    //========================

    event Approval(IToken indexed _token, address indexed user, address indexed spender, uint256 amount);
    event Send(IToken indexed _token, address indexed sender, address indexed receiver, uint256 amount, bool intern);
    event Receive(IToken indexed _token, address indexed sender, address indexed receiver, uint256 amount, bool intern);   

    //========================
    // CONFIG FUNCTIONS
    //========================

    constructor(IMoonAccessManager _accessManager)
    MoonAccessRequest(
        _accessManager,
        address(0)
    )
    {

    }

    //========================
    // CONFIG FUNCTIONS
    //========================

    function setTransferGas(uint256 _gas) external
    {
        requireManager();
        _setTransferGas(_gas);
    }

    function preventTransfer(address _user) external
    {        
        //This functions poses a centralization risk.
        //Basicaly it freezes the assets of a user,
        //while also preventing transfers via allowance
        //from his account or from others as spender.
        //Should only be used to punish malicious accounts!
        requireSecurityAdmin();
        userInfo[_user].preventTransfer = true;
    }

    function allowTransfer(address _user) external
    {
        requireSecurityAdmin();
        userInfo[_user].preventTransfer = false;
    }

    function setEnableAllowance(bool _enable) external
    {
        requireManager();
        enableAllowance = _enable;
    }
    
    //========================
    // USER INFO FUNCTIONS
    //========================

    function balanceOf(IToken _token, address _user) public view returns (uint256)
    {
        return userInfo[_user].tokens[_token].balance;
    }

    function allowance(IToken _token, address _user, address _spender) public view returns (uint256)
    {
        return userInfo[_user].tokens[_token].allowance[_spender];
    }   

    function getTokenListLength(address _user) external view returns (uint256)
    {
        return userInfo[_user].tokenList.length();
    }

    function getAssetInfoInRange(address _user, uint256 _from, uint256 _to) external view returns (AssetInfo[] memory)
    {
        //get range excluding _to
        requireValidRange(
            userInfo[_user].tokenList.length(), 
            _from, 
            _to
        );
        AssetInfo[] memory list = new AssetInfo[](_to - _from);
        for (uint256 n = _from; n < _to; n++)
        {
            uint256 p = n - _from;
            IToken t = IToken(userInfo[_user].tokenList.at(n));
            list[p].token = t;
            list[p].balance = balanceOf(t, _user);
            list[p].allowances = getAllowanceListLength(_user, t);
        }
        return list;
    }

    function getUserInfo(address _user) external view returns (AssetInfo[] memory)
    {
        uint256 to = userInfo[_user].tokenList.length();
        AssetInfo[] memory list = new AssetInfo[](to);
        for (uint256 n = 0; n < to; n++)
        {
            IToken t = IToken(userInfo[_user].tokenList.at(n));
            list[n].token = t;
            list[n].balance = balanceOf(t, _user);
            list[n].allowances = getAllowanceListLength(_user, t);
        }
        return list;
    }

    function getAllowanceListLength(address _user, IToken _token) public view returns (uint256)
    {
        return userInfo[_user].tokens[_token].allowanceList.length();
    }

    function getAllowancesInRange(address _user, IToken _token, uint256 _from, uint256 _to) external view returns (address[] memory)
    {
        //get range excluding _to
        requireValidRange(
            userInfo[_user].tokens[_token].allowanceList.length(), 
            _from, 
            _to
        );
        address[] memory list = new address[](_to - _from);
        for (uint256 n = _from; n < _to; n++)
        {
            list[n - _from] =  userInfo[_user].tokens[_token].allowanceList.at(n);
        }
        return list;
    }

    function getUserAllowanceInfo(address _user, IToken _token) external view returns (AllowanceInfo[] memory)
    {
        uint256 to = getAllowanceListLength(_user, _token);
        AllowanceInfo[] memory list = new AllowanceInfo[](to);
        for (uint256 n = 0; n < to; n++)
        {
            address spender = userInfo[_user].tokens[_token].allowanceList.at(n);
            list[n].spender = spender;
            list[n].allowance = allowance(_token, _user, spender);
        }
        return list;
    }

    //========================
    // DEPOSIT FUNCTIONS
    //========================

    function depositETHFor(address _user) external payable nonReentrant
    {
        //check
        require(msg.value > 0, "Transfer 0");

        //deposit
        increaseBalance(
            COIN_ADDRESS,
            _user,
            msg.sender,
            msg.value,
            false
        );
    }

    function depositFor(IToken _token, address _user, uint256 _amount) external nonReentrant
    {
        //check
        require(_amount> 0, "Transfer 0");
        require(address(_token) != address(COIN_ADDRESS), "Can't deposit coin");

        //receive
        uint256 received = safeTransferFrom(
            _token,
            _amount,
            msg.sender,
            address(this)
        ).transferred;

        //deposit
        increaseBalance(
            _token,
            _user,
            msg.sender,
            received,
            false
        );        
    }    

    //========================
    // TRANSFER FUNCTIONS
    //========================

    function transfer(IToken _token, address _from, address _to, uint256 _amount) external nonReentrant whenNotPaused
    {
        //check
        requireTransferAllowed(_from);
        checkTransferAllowance(_token, _from, _amount);

        //withdraw     
        decreaseBalance(
            _token,
            _from,
            _to,
            _amount,
            false
        );

        //transfer
        if (_token == COIN_ADDRESS)
        {            
            transferETH(_to, _amount);
        }
        else
        {
            safeTransfer(
                _token,
                _amount,
                _to
            );
        }
    }

    function transferToAccount(IToken _token, address _from, address _to, uint256 _amount) external nonReentrant whenNotPaused
    {
        //check
        requireTransferAllowed(_from);
        checkTransferAllowance(_token, _from, _amount);
        require(_from != _to, "Can't transfer to yourself");
        
        //withdraw
        decreaseBalance(
            _token,
            _from,
            _to,
            _amount,
            true
        );

        //deposit
        increaseBalance(
            _token,
            _to,
            _from,
            _amount,
            true
        );
    }

    function checkTransferAllowance(IToken _token, address _from, uint256 _spendAmount) private
    {
        if (_from != msg.sender)
        {
            //check allowance enabled
            require(enableAllowance, "Allowance disabled");

            //check allowance amount
            uint256 curAllowance = userInfo[_from].tokens[_token].allowance[msg.sender];
            require(curAllowance >= _spendAmount, "Transfer amount exceeds allowance");
            userInfo[_from].tokens[_token].allowance[msg.sender] -= _spendAmount;
        }
    }

    function increaseBalance(
        IToken _token, 
        address _user,
        address _sender, 
        uint256 _amount,
        bool _internal
    ) private 
    {
        //index
        if (userInfo[_user].tokens[_token].balance == 0
            && _amount > 0)
        {
            userInfo[_user].tokenList.add(address(_token));
        }

        //balance
        userInfo[_user].tokens[_token].balance += _amount;
        totalTokenBalance[_token] += _amount;

        //event
        emit Receive(
            _token,
            _user,
            _sender,
            _amount,
            _internal
        );
    }

    function decreaseBalance(
        IToken _token, 
        address _user, 
        address _sender,
        uint256 _amount,
        bool _internal
    ) private 
    {
        //check
        require(userInfo[_user].tokens[_token].balance >= _amount, "Insufficient balance");

        //balance
        userInfo[_user].tokens[_token].balance -= _amount;
        totalTokenBalance[_token] -= _amount;

        //index
        if (userInfo[_user].tokens[_token].balance == 0
            && _amount > 0)
        {
            userInfo[_user].tokenList.remove(address(_token));
        }

        //event
        emit Send(
            _token,
            _user,
            _sender,
            _amount,
            _internal
        );
    }

    //========================
    // ALLOWANCE FUNCTIONS
    //========================

    function approve(IToken _token, address _spender, uint256 _amount) external
    {
        _approve(
            _token,
            msg.sender, 
            _spender, 
            _amount
        );
    }

    function _approve(IToken _token, address _user, address _spender, uint256 _amount) private
    {
        //check
        require(_user != address(0), "User is zero address");
        require(_spender != address(0), "Spender is zero address");

        //index
        UserAssetInfo storage info = userInfo[_user].tokens[_token];
        if (info.allowance[_spender] != _amount)
        {
            if (info.allowance[_spender] == 0)
            {
                info.allowanceList.add(_spender);
            }
            else if (_amount == 0)
            {
                info.allowanceList.remove(_spender);
            }
        }        

        //set
        info.allowance[_spender] = _amount;

        //event
        emit Approval(
            _token, 
            _user, 
            _spender, 
            _amount
        );
    }

    function increaseAllowance(IToken _token, address _spender, uint256 _amount) external 
    {
        _approve(
            _token,
            msg.sender, 
            _spender, 
            allowance(_token, msg.sender, _spender) + _amount
        );
    }

    function decreaseAllowance(IToken _token, address _spender, uint256 _amount) external
    {
        //check
        uint256 curAllowance = allowance(_token, msg.sender, _spender);
        require(curAllowance >= _amount, "Insufficient allowance");

        //decrease
        _approve(
            _token,
            msg.sender, 
            _spender, 
            curAllowance - _amount
        );
    } 

    //========================
    // HELPER FUNCTIONS
    //========================

    receive() external payable {}  

    function requireValidRange(
        uint256 _length, 
        uint256 _from, 
        uint256 _to
    ) internal pure
    {
        require(_to <= _length, "Index is out of bounds");
        require(_to > _from, "End index must be greater than start index");
    }

    function requireTransferAllowed(address _from) private view
    {
        require(!userInfo[msg.sender].preventTransfer, "Transfer prevented for spender");
        require(!userInfo[_from].preventTransfer, "Transfer prevented from account");
    }

    //========================
    // EMERGENCY FUNCTIONS
    //======================== 

    function recoverETH(uint256 _amount, address _to) external
    {
        //check
        requireSecurityAdmin();
        require(address(this).balance - totalTokenBalance[COIN_ADDRESS] >= _amount, "Can't access used funds");

        //recover
        _recoverETH(_amount, _to);        
    }

    function recoverToken(IToken _token, uint256 _amount, address _to) external
    {
        //check
        requireSecurityAdmin();
        require(_token.balanceOf(address(this)) - totalTokenBalance[_token] >= _amount, "Can't access used funds");

        //recover
        _recoverToken(_token, _amount, _to);
    }  
}